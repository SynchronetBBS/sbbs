project (ConIOLib C)

cmake_minimum_required(VERSION 3.11)

set(WITHOUT_GDI OFF CACHE BOOL "Disable GDI video support")
set(WITHOUT_SDL OFF CACHE BOOL "Disable SDL video support")
set(WITHOUT_X11 OFF CACHE BOOL "Disable X11 video support")
set(WITHOUT_XRANDR OFF CACHE BOOL "Disable X11 video support")
set(WITHOUT_XRENDER OFF CACHE BOOL "Disable X11 video support")
set(WITHOUT_XINERAMA OFF CACHE BOOL "Disable X11 video support")

INCLUDE (CheckFunctionExists)
if(NOT WITHOUT_X11)
	find_package(X11)
endif()
# So on BSD, this causes the find to fail.  However, it's required for Linux
# This is because on BSD, ncursesw.h does not exist, but libraries use
# ncurses.h
set(CURSES_NEED_WIDE TRUE)
find_package(Threads REQUIRED)
if(NOT WIN32)
	find_package(Curses)
	find_package(PkgConfig)
	if(NOT WITHOUT_SDL)
		pkg_check_modules(SDL2 sdl2)
	endif()
	if(NOT WITHOUT_X11)
		if(NOT WITHOUT_XRENDER)
			pkg_check_modules(XRENDER xrender)
		endif()
		if(NOT WITHOUT_XINERAMA)
			pkg_check_modules(XINERAMA xinerama)
		endif()
		if(NOT WITHOUT_XRANDR)
			pkg_check_modules(XRANDR xrandr)
		endif()
	endif()
endif()

# TODO: Don't require allfonts.c when building without bitmap support
set(SOURCE
	allfonts.c
	ansi_cio.c
	ciolib.c
	ciolib.rc
	cterm.c
	mouse.c
	syncicon64.c
	utf8_codepages.c
	vidmodes.c
)

set(HEADER
	ciolib.h
	cterm.h
	keys.h
	mouse.h
	utf8_codepages.h
	vidmodes.h
)

if(WIN32)
	if(NOT WITHOUT_GDI)
		list(APPEND SOURCE win32cio.c)
	endif()
	list(APPEND SOURCE ciolib.rc)
endif()

if(NOT ${CMAKE_SYSTEM_NAME} MATCHES "Windows")
	if(CURSES_FOUND)
		list(APPEND SOURCE curs_cio.c)
		set(USE_CURSES_ANYWAY TRUE)
	else()
		if(${CMAKE_SYSTEM_NAME} MATCHES "(Free|Open|Net)BSD")
			list(APPEND SOURCE curs_cio.c)
			set(USE_CURSES_ANYWAY TRUE)
		else()
			message(FATAL_ERROR "ncursesw not found")
		endif()
	endif()
endif()

if(NOT WITHOUT_X1!)
	if(X11_FOUND)
		list(APPEND SOURCE x_events.c x_cio.c)
		set(NEED_BITMAP TRUE)
		set(NEED_DLOPEN TRUE)
		set(NEED_SCALE TRUE)
	endif()
endif()

if(NOT WITHOUT_SDL)
	if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
		if(NOT SDL2_FOUND)
			find_library(SDL2_LIBRARY SDL2)
			if(SDL2_LIBRARY)
				set(SDL2_FOUND ON)
			endif()
		endif()
	endif()
	if(SDL2_FOUND)
		list(APPEND SOURCE sdl_con.c)
		list(APPEND SOURCE sdlfuncs.c)
		set(NEED_SCALE TRUE)
		if(WIN32)
			list(APPEND SOURCE SDL_win32_main.c)
		endif()
		set(NEED_BITMAP TRUE)
		set(NEED_DLOPEN TRUE)
	endif()
endif()

if(NEED_BITMAP)
	list(APPEND SOURCE bitmap_con.c)
endif()

if(NEED_SCALE)
	list(APPEND SOURCE scale.c xbr.c)
endif()

add_library(ciolib OBJECT ${SOURCE})
target_include_directories(ciolib PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
if(CURSES_FOUND)
	if(${CMAKE_SYSTEM_NAME} MATCHES "(Free|Open|Net)BSD")
		list(APPEND SOURCE curs_cio.c)
		set(USE_CURSES_ANYWAY TRUE)
	endif()
endif()

if(WITHOUT_X11)
	target_compile_definitions(ciolib PRIVATE NO_X)
	target_compile_definitions(ciolib PUBLIC DISABLE_X11=1)
else()
	if(X11_FOUND)
		target_include_directories(ciolib PRIVATE ${X11_INCLUDE_DIR})
		if(NOT WITHOUT_XRENDER)
			if(XRENDER_FOUND)
				target_compile_definitions(ciolib PUBLIC WITH_XRENDER)
			endif()
		endif()
		if(NOT WITHOUT_XINERAMA)
			if(XINERAMA_FOUND)
				target_compile_definitions(ciolib PUBLIC WITH_XINERAMA)
			endif()
		endif()
		if(NOT WITHOUT_XRANDR)
			if(XRANDR_FOUND)
				target_compile_definitions(ciolib PUBLIC WITH_XRANDR)
			endif()
		endif()
	else()
		target_compile_definitions(ciolib PRIVATE NO_X)
		target_compile_definitions(ciolib PUBLIC DISABLE_X11=1)
		message(WARNING "libx11 not found, X11 support disabled")
		set(WITHOUT_X11 ON CACHE BOOL "Disable X11 video support")
	endif()
endif()

if(NOT WITHOUT_SDL)
	if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
		if(SDL2_LIBRARY)
			target_link_libraries(ciolib ${SDL2_LIBRARY})
			target_include_directories(ciolib PUBLIC ${SDL2_LIBRARY}/Headers)
			target_include_directories(ciolib PUBLIC ${SDL2_LIBRARY})
			target_compile_definitions(ciolib PUBLIC STATIC_SDL)
			target_compile_definitions(ciolib PUBLIC WITH_SDL)
		endif()
	else()
		if(SDL2_FOUND)
			target_include_directories(ciolib PUBLIC ${SDL2_INCLUDE_DIRS})
			set(WITH_SDL TRUE PARENT_SCOPE)
			target_compile_definitions(ciolib PUBLIC WITH_SDL)
		endif()
	endif()
endif()

if(WIN32)
	if(NOT WITHOUT_GDI)
		target_compile_definitions(ciolib PUBLIC WITH_GDI)
	endif()
else()
	target_link_libraries(ciolib pthread)
endif()

if(USE_CURSES_ANYWAY)
	if(CURSES_FOUND)
		if (CURSES_HAVE_NCURSES_NCURSES_H)
			target_compile_definitions(ciolib PRIVATE DEBIAN_HATES_YOU)
		elseif(CURSES_HAVE_NCURSES_H)
			target_compile_definitions(ciolib PRIVATE N_CURSES_LIB)
		endif()
		target_link_libraries(ciolib ${NCURSES_LIBRARIES})
	else()
		if(NOT CURSES_NCURSES_LIBRARY)
			target_link_libraries(ciolib ${CURSES_CURSES_LIBRARY})
		else()
			target_link_libraries(ciolib ${CURSES_NCURSES_LIBRARY})
		endif()
		if(CURSES_TINFO_LIBRARY_NAME)
			if(NOT CURSES_NCURSES_HAS_CBREAK OR NOT CURSES_NCURSES_HAS_NODELAY)
				find_library(CURSES_EXTRA_LIBRARY "${CURSES_TINFO_LIBRARY_NAME}")
				if(CURSES_EXTRA_LIBRARY)
					target_link_libraries(ciolib ${CURSES_TINFO_LIBRARY_NAME})
				endif()
			endif()
		endif()
	endif()
endif()

CHECK_FUNCTION_EXISTS(vasprintf HAVE_VASPRINTF) 
if(HAVE_VASPRINTF)
	target_compile_definitions(ciolib PRIVATE HAVE_VASPRINTF)
endif()
target_link_libraries(ciolib hash encode xpdev)
