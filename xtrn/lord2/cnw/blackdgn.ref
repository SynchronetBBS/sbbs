; Script file BLACKDGN.REF, indexed by REFINDEX v0.01 on Sun Mar 18 22:19:28 2001

@DECLARE locksign 000000457
@DECLARE bdcsign 000000527
@DECLARE pensign 000000605
@DECLARE mybank 000000665
@DECLARE tower 000003408
@DECLARE doorguard 000003517
@DECLARE startg 000003546
@DECLARE outsign 000007086
@DECLARE enter 000007414
@DECLARE done 000008573
@DECLARE letmein 000010343
@DECLARE done 000010420
@NOCHECK
; End of .REF index

@#locksign
@do saybar
`2Sign reads: `0 Bob's Lockup
@do moveback

@#BDCSIGN
@do saybar
`2Sign reads: `0 Welcome to my Castle
@do moveback

@#pensign
@do saybar
     `2Cheap Slaves!
@do moveback

@#MYBANK
;team treasury
@do moveback
@do `p20 is 0
@if dragtres.dat exist true then do
  @begin
  @readfile dragtres.dat
`p20
  @
  @end

@clearblock 20 21
@do move 3 21
@do write
`r0`2  Treasury is at `$$`p20`2 - (`0T`2)ake, (`0L`2)eave or (`0N`2)evermind? [`0N`2] :`%`x
@do readspecial `s01 NTL
@if `s01 is N then do
 @begin
 @do saybar
You close the safe, carefully.
 @closescript
 @end

@if `s01 is T then do
  @begin
  @do move 5 23
  @do write
`0`r0Amount? `0: `%
 @do readnum 10 0
  @do `p21 is `v40
  @if `p21 < 1 then `p21 is 0
  @if `p21 > `p20 then do
    @begin
    @clearblock 21 24
    @do saybar
Not enough in here for that.
    @closescript
    @end

 @if `p21 is 0 then do
   @begin
    @clearblock 21 24
    @do saybar
You take nothing.
    @closescript
   @end
//withdrawl.. we read from the file again so cheating won't be possible with
//correct timing
  @readfile dragtres.dat
`p20
  @

  @if `p20 < `p21 then do
    @begin
    @clearblock 21 24
    @do saybar
Oh no!  Your money is gone! Think about it!
    @closescript
    @end

    @do `p20 - `p21
    @do money + `p21
  @do delete dragtres.dat
  @writefile dragtres.dat
`p20
    @clearblock 21 24
    @do saybar
`2You take `$`p21`2 from the treasury.

//if not owner, lets notify him

@readfile blackdgn.dat
`p20
`s01
@if `p20 not &playernum then do
  @begin
   @writefile mail\mail`p20.dat
  `0Black Dragon Castle Reports:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 has taken `$$`p21 `2from the treasury.

  @end
  @closescript
  @end

@if `s01 is L then do
  @begin
  @do move 5 23
  @do write
`0`r0Amount? `0: `%
 @do readnum 10 &money
  @do `p21 is `v40
  @if `p21 < 1 then `p21 is 0
  @if `p21 > money then do
    @begin
    @clearblock 21 24
    @do saybar
You are Broke you mother!
    @closescript
    @end

 @if `p21 is 0 then do
   @begin
    @clearblock 21 24
    @do saybar
        Cheap Bastard!
    @closescript
   @end

  @do money - `p21

//deposit.. we read from the file again so cheating won't be possible with
//correct timing
  @readfile dragtres.dat
`p20
  @
  @do `p20 + `p21
  @do delete dragtres.dat
  @writefile dragtres.dat
`p20
  @
    @clearblock 21 24
//if not owner, lets notify him

@readfile blackdgn.dat
`p20
`s01
@
;load owner and current password

@if `p20 not &playernum then do
  @begin
   @writefile mail\mail`p20.dat
  `0Black Dragon Castle Reports:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 has deposited `$$`p21 `2to the treasury.

  @end


    @do saybar
`2You leave `$`p21`2 in the treasury.
  @closescript


  @end


@#TOWER
;Tower in the castle
@DO SAYBAR
`2The tower on this castle is not ready yet.
@do moveback

@#doorguard
@do moveback
@label startg
;guy who owns on it pushes against the door guard symbol.  This lets him
;control the password to gain entrance to the castle, as well as sell it
;back or transfer ownership.
@if blackdgn.dat exist false then do
  @begin
  @do saybar
`0"You have 5 minutes to vacate this keep!" `2the guard barks.
  @closescript
  @end

@readfile blackdgn.dat
`p20
`s01
@
;load owner and current password

@if `p20 not &playernum then do
  @begin
  @do saybar
You wave to the door guard, nice of him to let you in.
  @closescript
  @end
@busy


@do `v01 is 1 ;set default choice to 1
@show
`r0`c  `r1 Jerking off your servants `r0

  `2The huge doorguard snaps his bra strap when he sees you are going to
  address him.

@do write
`x`x
@choice
Set password to gain entrance (currently is `0`s01`%)
Ownership options
Its my home
Leave
@if responce is 3 then do
        @begin
        @do `p69 is 31
        @show
`\`$Black Dragon Castle is your new home.`\
        @key
        @do goto startg
        @end
@if responce is 2 then do
 @begin
 @show
`r0`c  `r1 Ownership Options `r0

  `2You retrieve your deed and carefully look it over.
   
   @do `v01 is 1
   @do write
`x`x
   @choice
Transfer deed to someone else
Sell property back to the bank for `$50,000`%.
Do nothing of the sort
 @if response is 1 then do
   @begin
   @show
`\`\  `2Transfer to whom?
   @chooseplayer `p20

   @if `p20 is 0 then do
    @begin
    @show
  `2You decide to keep your domain - for now.`\`\`k
    @do goto startg
    @end
   @do `s02 is getname `p20
   @show
`2  Are you sure you wish to give your keep to `0`s02`2?
@do write
`x`x
@choice
Yes
Not on your life.
 @if responce is 2 then do
   @begin
    @show
`\`\  `2You decide to keep your domain - for now.`\`\`k
    @do goto startg
   @end

  @do delete blackdgn.dat
  @writefile blackdgn.dat
`p20
`s01
   @writefile mail\mail`p20.dat
  `0Business note:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 has transferred ownership of Black Dragon Castle to you. 
  Password to enter will remain `0"`s01"`2 unless
  `2you come by and change it.

    @do addlog
  `0`n`2 has given `%Runion Keep`2 to `0`s02`2!
     @routine seperate in gametxt.ref

    @show
`\`\  `2Deed transfered.  Oh well - it was fun while it lasted.`\`\`k
   @do goto done
   @end
  @if response is 2 then do
    @begin
    @show
`\`\`2  Are you sure you wish to sell your Castle back to the bank?`\
@do `v01 is 1
@do write
`x`x
@choice
Yes
Not on your life.
 @if responce is 2 then do
   @begin
    @show
`\`\  `2You decide to keep your domain - for now.`\`\`k
    @do goto startg
   @end

  @do delete blackdgn.dat
  @do money + 50000
  @show
`\`\  `%CASTLE SOLD!`2`\`\`k
    @do addlog
  `0`n`2 has sold `%Black Dragon Castle`2 back to the bank!
    @routine seperate in gametxt.ref
    @do goto done
    @end


  @do goto startg
 @end

@if responce is 1 then do
  @begin
  @do write
`\`\  `0"And the new password, s&sir?" `2(enter `4NONE`2 for no access to anyone)`\  `2Password:`x
  @do readstring 40 `s01 `s01 
  @do `p20 is length `s01
  @if `p20 < 2 then do
   @begin
   @show
`\`\  `2You decide the current password is ok.`\`\`k
   @do goto startg
   @end
  @do delete blackdgn.dat
  @writefile blackdgn.dat
&playernum
`s01
@show
`\`\  `0"Yes s&sir, I will not forget it!" `2the guard promises.`\`\`k
   @do goto startg
  @end

@drawmap
@update

@#outsign
@do moveback

 @if blackdgn.dat exist false then do
 @begin
 @do saybar
`6Black Dragon Castle `0- For sale cheap, enquire inside.
 @end 

@if blackdgn.dat exist true then do
 @begin
@readfile blackdgn.dat
`p20
`s01
@
@do `s01 is getname `p20
 @do saybar
`2Property of `0`s01`2. So leave now!
 @end 

@#enter
@do moveback
@offmap
@if blackdgn.dat exist false then do
  @begin
  @show
`r0`c  `r1 `%For Sale `r0

  `2Black Dragon Castle was once a magnificient structure that housed many of
  the royal families.  Now it houses dust and decay. And tyre irons.

  It is for sale, however.  The asking price is `$$100,000`2.

@do `v01 is 1
@do write
`x`x
@choice
>&money 99999 Purchase it.
<&money 100000 Sigh wistfully and leave since you don't have enough.
>&money 99999 Leave.
@

@if responce is 1 then do
  @begin
  @if blackdgn.dat exist true then do
    @begin
    @show
`\`\  `2Oh crap!  Someone bought it while you were thinking about it.  You
  will regret your snail like thinking for the rest of your days.`\`\`k
    @do goto done
    @end

  @writefile blackdgn.dat
&playernum
NONE
    @do money - 100000
    @do addlog
  `0`n `2buys `%Black Dragon Castle`2!
     @routine seperate in gametxt.ref

  @show
`r0`c  `r1 `%Why hello there, landowner! `r0
`\`2  Nice job!  You now own this keep!

`k  `0As a special bonus, Many beds and a door guard are included!

  `2Enjoy your new home.`\`\`k
  @end

@label done
@drawmap
@update
@closescript
  @end

@readfile blackdgn.dat
`p20
`s01
@

@do `p21 is deleted `p20

@if `p21 is 1 then do
  @begin
   ;owner got deleted!!!  Lets put it up for sale again.
   @do delete blackdgn.dat
    @do addlog
  `%Black Dragon Castle`2 is put on the market today.
     @routine seperate in gametxt.ref

   ;run script again
   @do goto enter
  @end

@if `p20 not &playernum then do
  @begin
  @do `s02 is getname `p20
  @do upcase `s01
  @if `s01 is NONE then do
   @begin
   @show
`r0`c  `r1 None shall pass `r0

  `2You pound on the door for some time before you get an answer.

  `0"Master `s02`0 has declared that `4NO`0 visitors shall be allowed!" `2a
  voice calls from behind the wall.

  `4Ouch!

`k
   @do goto done
   @end

  @show
`r0`c  `r1 Stand and unfold thyself `r0

`2  A head pops over the gate.  `0"Halt, you!  What business have you with
  my lord and master, `%`s02`0?" `2the door guard asks.

@do write
`x`x
@do `v01 is 1
@choice
Offer an explanation.
Visit later.
  @if response is 1 then do
   @begin
   @do write
`\`\  `2Say what? :`x
  @do readstring 40 NIL `s03 
  @do upcase `s03
  @do upcase `s01

  @if `s03 is `s01 then do
    @begin
    @show
`\`\  `0"Welcome to `%Black Dragon Castle`0,"`2 the guard smiles as he opens the gate.`\`\`k
    @do goto letmein
    @end
   @writefile mail\mail`p20.dat
  `0Black Dragon Castle Reports:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 attempted to sweet talk the guard by saying
  `0"`s03"`2.  `2He was `bNOT`2 allowed in.

   @show
`\`\  `2The guard doesn't look too interested.`\`\`k
   @do goto done
   @end

  @do goto done

  @end

;let him in
@label letmein
@do x is 42
@do y is 19
@do map is 30
@loadmap 30
@do goto done
@#done
