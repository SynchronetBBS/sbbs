; Script file PORTTOWN.REF, indexed by REFINDEX v0.01 on Sun Mar 18 22:19:14 2001

@DECLARE showwhore 000001198
@DECLARE junkman1 000006459
@DECLARE junkman2 000006653
@DECLARE fish 000006803
@DECLARE fishsell 000007433
@DECLARE man 000008296
@DECLARE whore 000009152
@DECLARE ponward 000009947
@DECLARE psleep 000011468
@DECLARE inn 000012270
@DECLARE potiond 000014360
@DECLARE heal 000018468
@DECLARE pirate 000020185
@DECLARE cargo 000020917
@DECLARE travel 000022619
@DECLARE mart 000024444
@DECLARE martstart 000024522
@DECLARE bank 000026291
@DECLARE bankstart 000026418
@DECLARE bankagain 000027524
@DECLARE deposit 000028977
@DECLARE withdraw 000029525
@DECLARE hang 000030387
@DECLARE notdone 000030588
@DECLARE fishdock 000030686
@DECLARE fishcast 000031302
@DECLARE removeline 000034632
@DECLARE waterfall 000035144
@DECLARE watersign 000035226
@DECLARE trees 000035323
@DECLARE shrine 000035423
@DECLARE done 000036775
@DECLARE nomore 000036830
@NOCHECK
; End of .REF index

;porttown and surrounding areas
;pap and seth both worked on this file, so if the spacing is different
;in places (ie, ugly as hell) this was paps work. ;>
@#showwhore
@show
[2J[0;1;30;47m∞[0mﬂﬂ[34m        ‹  ‹‹ [37mﬂﬂ€[1;30;47m∞∞∞[0m€€ﬂ 
[A[27C[34m ‹ ‹‹ﬂﬂﬂ€€[1;44m‹‹‹‹[0;34m€‹ [37mﬂ€[1;30;47m∞∞[0m€€€€€€
[A[54Cﬂﬂ€€ﬂﬂ€€€€€€ﬂ€€€[1;47m∞∞∞‹‹‹‹‹[0m€
[A[79C [34m ‹€ﬂﬂﬂ       ∞∞   ˛ [37mﬂ[1;30;47m∞[0mﬂ[34m‹‹ﬂ  ∞∞   ‹€
[A[35C[1;44m‹€€€≤≤±[0;36;44m∞[34;40m€‹ [37mﬂ€€€€€[1;30;47m ∞[0m €
[A[56Cﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ€[1;47m€[0m€
[A[79C [36;44m∞[34;40m€[1;44m‹‹[0;34m€€‹‹        ∞∞ ﬂ‹ ﬁ≤∞  [4C‹€
[A[33C[1;44m‹€€€≤≤±±∞∞[0;34m€€€ [37mﬁ€€€€€[1;30;47m∞[0;34m [37m€
[A[56C[20C€[1;47mﬁ[0m€
[A[79C [1;34;44m≤€€€€€‹‹‹[0;34m€‹‹  ‹‹ [3C ≤˛ﬂ ‹‹ﬂﬂﬂﬂﬂ€€[1;44mﬂ€≤±±[40m
[A[39C[44m∞∞[0;34m€€€[30;44m∞∞[34;40m± [37m€€€€€€[34m [1;30;47m∞[40m
[A[56C[47m[0;34m  [37mYou think you   [2C[1;30;47m∞[37mﬁ[0m€
[A[79C [1;34;44m±≤≤≤€€€€€≤∞[0;34mﬂﬂﬂ    ﬂﬂ‹ﬂ  ﬂ[9Cﬂﬂ[36;44m∞[1;34m[40m
[A[37C[44m±∞[0;34m€€€[30;44m∞∞[34;40m±∞  [37mﬁ€€€[1;47m∞[0m€ﬂ
[A[55C[1;30;47m∞[0;34m  [37m                 [C[1;30;47m∞[37mﬁ[0m€
[A[79C [1;34;44m∞∞±±≤≤≤€≤∞[0;34mﬂ[37m  [33m‹‹€[1;31;43m ‹‹[0;33m‹
[A[20C‹ﬂﬂ€[1;31;43m∞‹‹‹‹‹[40m‹‹[0;33m‹‹[37m  [34mﬁ€€[30;44m∞∞∞[40m
[A[42C[44m[34;40m±∞  ›  [37mﬂ[1;47m‹€‹[0m€[34m [1;30;47m∞[2C[40m
[A[58C[47m[0mknow what this[4C[1;30;47m∞[37m∞[0m€
[A[79C [34m€€[1;44m∞∞±±±≤[0;34m€›[37m [33m‹€[1;31;43m∞‹[40m€€€€€
[A[20C€€€‹‹ﬂﬂ€€€€€[43m€[40m€[43m‹∞[0m [34m€[30;44m∞[34;40m±∞∞   
[A[45C€[3C [37mﬁ[1;47mﬂ[0m€€[34m [37m€[34m  [1;35m             
[A[71C   [2C[0m€[1;47m ∞[40m
[A[79C[0m [34m≤≤€€[1;44m∞∞∞ﬂ[0;34mﬂ[6C[1;31mﬂﬂﬂﬂ€€€€€€€€‹‹ﬂﬂ€€€€
[A[35C[43m›[0;33m›[34mﬁ€∞[5C›∞    [37m€€€[34m [37m€[1;35m [37m
[A[57C [0mwoman wants even[2C€[1;47m ﬁ[40m
[A[79C[0m [34m∞±≤≤≤ﬂ[5C[1;31m [0;33mﬂ€[1;31;43m∞≤[40m€‹‹‹  ﬂﬂ€€€
[A[27C€€€€‹ﬂﬂﬂ[3C[0;34m€›[4C[36;44m∞[34;40m∞[3C  [37mﬁ€€[34m 
[A[55C[1;37;47m∞[40m                 [3C[0m€[1;47m ﬁ[40m
[A[79C[0m [34m ∞∞∞≤›[37m ‹‹›[1;31m [34m‹[4C[31mﬂﬂﬂﬂ€€€‹‹€€€€€€
[A[31C[0m  [1;30m‹≤ﬂ[0m [1;30m∞[0;34m €  [7C  [37mﬁ€€ﬂ[1;30;47m[40m
[A[55C[47m∞[2C[0mbefore she asks.[2C[1;30;47m∞[37m∞ﬁ[40m
[A[79C[0m [3C[34m∞∞›[37m [1;47m∞≤[31;40m [36;44m∞[0;34m›[1;37m
[A[12C˛ﬂ[31m  [34m∞[0;34mﬁ[1;44m∞[0m ‹ [1;31m ﬂ€€€€€€€›[0m ∞
[A[34C[36m [4C[34mﬁ€[7C∞  [37m€[1;47m‹[0m€[34m [1;30;47m∞[20C[40m
[A[76C[47m∞[37m ﬁ[40m
[A[79C[0m [4C[34m  [37m [1;47m≤€[40m›[31m [0;34mﬂ[1;36m∞[0m  
[A[15C[1;34m [0;34m‹ﬂ[1;31m [0m‹[1;47m∞[0mﬂﬂ[1;31m ‹€€€€€€€[0m 
[A[33C[36m∞[1;30m›[0;31m [4C[34mﬂ€‹∞[5C [37mﬁ€[1;47m›[0m€[34m
[A[54C [1;30;47m∞[2C[37;40m <press a key> [3C[30;47m∞[37m ﬁ[40m
[A[79C[0m [4C[34m∞ [37m [33m‹[37m [1mﬂ[9C[31m‹‹‹€€€[43m€[40m
[A[26C€[43m€≤[40m€€[43m≤[40m›[0m [1;30m∞[7C[0;34m ﬂﬂ‹    [37mﬁ
[A[51C€[1;47m›[0m€€€[20C€[1;47m ﬁ[40m
[A[79C[0m [3C[34m∞› [33m€[1;31;43m∞≤[40m€€€‹‹€€€€ﬂﬂ‹€€€€€€€€€€€
[A[32C[43m∞[40m [0m [1;30m∞[0;34m [9C€   [1;37;47m  ›[0m€ﬂ€
[A[56C[20C€[1;47m ≤[40m
[A[79C[0m [34m  ∞ﬁ› [33m∞≤[1;31;43mﬂ[40m€‹‹ﬂﬂﬂ‹€€€€€€€[43m€€[40m
[A[25C€€[47m€€[40m€€€€[43m‹[0;33m‹‹‹[1;31m   [0;31m [37m [33m‹≤
[A[44C [37m [34mﬁ›  [1;37;47m‹[40m€[47m› [0m ﬂﬂﬂﬂﬂﬂﬂ€€ﬂﬂﬂﬂﬂ€€€ﬂ€€
[A[75C€€[1;47m ﬁ[40m
[A[79C[0m [3C[34m∞ﬂ‹[37m [1;31m [0;33mﬂ≤[1;31;43m€[40m€€€€€
[A[16C[47m≤≤≤[40m€€€€€[43m€€[40m€€€[43m≤[40m€€€€€€[43m‹‹‹‹‹≤∞[40m
[A[43C[43m[0;33m›[34m ‹ﬂ  [1;37mﬁ€€[47m›   [0m€€[1;47m ‹‹€€€€€€‹[40m
[A[68C[47m‹        ‹€[40m
[A[79C[0m [5C[34m ﬂ‹ [33m∞[1;31;43m±[40m€€€[47m≤≤≤≤[40m€€€€€
[A[23C[43m€€€[40m€[47m€[43m€≤[40m€€€€€€[47m≤≤[40m€€€€[43m∞[0m 
[A[44C[34mﬁ≤   [1;37m€€€€[47m    ≤[40m€€€[47mﬂ∞ﬂ[40m€[47m€€[40m
[A[67C€€[47m€›∞   ‹[40m€[47m€[40m€
[A[79C[0m  [34m∞[4C ﬁ›[33m €[1;31;43m€[40m€€[47m≤≤[40m€€€€€€€€‹
[A[25Cﬂﬂﬂﬂ€€€€€[47m€€[40m€€€€€[43m≤[0;33m›[37m [34m≤∞   [1;37m€€
[A[51C€€€[47m‹‹[40m€€€€€€[47m‹[40m€€[47m€[40m€€€€[47m€‹‹‹€[40m€
[A[76C€[47m€€[40m
[A[79C[0m [6C[34m∞∞≤[33m ≤[1;31;43mﬁ[40m€€€[47m€€[40m€€€€€€€€€€
[A[27C€€€€€€€€€€€€€[43m≤[0;33m≤[1;31m [0;34mﬁ∞    [1;37m€€€€€€€€€€
[A[59C€€€€€€€€€€€€€€€[47m€€[40m€€€
[A[79C[0m [7C[34m≤›[33m [1;31;43m∞≤[40m€€€€€€€€€€ﬂﬂﬂﬂﬂﬂﬂ€€€€€€€€€
[A[38C€[43m≤[0;33m€∞[34m € [3C[37m [1mﬁ€€€€€€€€€€€€€€€€€€€€€€
[A[72C[47mﬂ[40m€€€€€€
[A[79C[0m [6C[34m‹ﬂ[33m ≤[1;31;43m≤[40m€€€€€[47m€[40m€ﬂ[37m 
[A[20C[31m‹[0;31m‹∞[1m [0;31m  [1m [0;31m [3C[1m ﬂﬂ€€€€[43m≤∞[40m
[A[40C[43m[0;33m∞[37m [34mﬂ[5C[37m [1mﬁ€€€€€€€€€€€€€€€€€€€€[47m[40m
[A[70C[47m≤[40m€€€€€€€€
[A[79C[0m [5C[34mﬁ›[33m ±[1;31;43m∞[40m€€€€€€€‹‹‹‹ [0;31mﬂﬂ˛ 
[A[26C[1m  [0;31m [1m ‹‹‹€€€€€[0;33mﬂ[37m  [34m∞[4C[37m [34m 
[A[48C[37m [1mﬁ€€€€€€€€€€€€€€€€€€€€€€€€€€€€€
[A[79C[0m [6C[34mﬂ‹ [33m∞ﬂ[1;31mﬂ€€€€€€€€€€[43m‹≤[0;33m‹‹[1;31m
[A[26C€€€€€€€€€ﬂﬂ[0m        ∞   [1m€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€
[A[79C[0m [6C[34m∞∞ﬂﬂ‹ [1;35m  [0m [1;31mﬂﬂﬂﬂﬂ€€€€€€€€ﬂﬂﬂ[8C[0m
[A[39C         [1mﬂﬂﬂﬂﬂ
@do readchar `s01

@#JUNKMAN1
@do `p70 is 49
@do moveback
@routine talk in talk.ref
@if `p20 is 0 then do
@begin
@do saybar
`0Nick: `2Ahh... Comfy chair!  Not like that one bed at Stonepass Lodge...
@end
@#JUNKMAN2
@do moveback
@busy
@show
`r0`c


                     `0"What would ya like to part with, kid?"

@sellmanager

@drawmap
@update

@#FISH
@offmap
@do moveback
@show
`r0`c  `r1  `%The House O' Fish  `r0

  `2People are buying and selling fish all over the place!  You also see
  some nice rods in a corner.  A man introduces himself as `0Smithy`2 and
  asks what you need. 

@do `v01 is 1
@do write
`x`x
@choice
Buy something
Sell some fish
Conduct business with Smithy
Leave
@if response is 3 then do
  @begin
  @do `p20 is 2
  @run getquest in quest.ref
  @closescript
  @end

@if response is 1 then do
  @begin
  @do write
`\`\
  @buymanager
  28
  29
  @do goto fish
  @end

@if responce is 2 then do
  @begin
  @label fishsell
  @show
`c  `r1  The market place  `r0

  `2You walk up to a fish bin and show 'em what you got.

  `2You are holding `$$&money`2.

@do write
`x`x
@do `v01 is 1
@choice
<`i29 1 <`i30 1 You have no fish to sell.
>`i29 0 Sell a Salmon for `$$50`%. `2(you have `0`i29`2)
>`i30 0 Sell a Sparking Green Salmon for `$$250`%. `2(you have `0`i30`2)
>`i29 0 <`i30 1 Sell nothing
>`i30 0 <`i29 1 Sell nothing
>`i30 0 >`i29 0 Sell nothing
@if responce is 2 then do
  @begin
  @show
`\`\  `0"Thank you for the fish, s&sir!"

`k
  @do money + 50
  @do `i29 - 1
  @do goto fishsell
  @end
@if responce is 3 then do
  @begin
  @show
`\`\  `0"My god?!? Where did you catch this?!?  Thank you!"

`k
  @do money + 250
  @do `i30 - 1
  @do goto fishsell
  @end

 @do goto fish

 @end

@drawmap
@update

;Helpfull man in Porttown
@#man
@do `p70 is 21
@do moveback
@routine talk in talk.ref
@if `p20 is 0 then do
@begin
@do moveback
@do `p20 random 9 1
@if `p20 is 1 then saybar
`%Guy: `0I hear the longer boat rides are relaxing.
@if `p20 is 2 then saybar
`%Guy: `0I hear there's a shortcut through the Snowy Pass!
@if `p20 is 3 then saybar
`%Guy: `0Watch out for the girls at the Hotel here...
@if `p20 is 4 then saybar
`%Guy: `0I hear the dark forest is a terrible place.
@if `p20 is 5 then saybar
`%Guy: `0A sailor told me of an undiscovered island once!
@if `p20 is 6 then saybar
`%Guy: `0The Hag's place is the safest place in town!
@if `p20 is 7 then saybar
`%Guy: `0Did you know Enya rules?
@if `p20 is 8 then saybar
`%Guy: `0On the other two continents I hear life is really exciting.
@if `p20 is 9 then saybar
`%Guy: `0Watch for the oracle!
@end

@#WHORE
;random things that happen walking around the sea hag hotel
@if sexmale is 0 then do
  @begin
  ;if they are female, don't run this... sorry girls, no female version
  ;was written.
  @closescript
  @end
@if `v06 is 1 then do
  @begin
  ;clean mode is on, lets get the hell out of this routine!!
  @closescript
  @end
@busy
@do write
`r1
@clearblock 21 21
@do move 3 21
@if ninn2.dat exist true then do
 @begin
 @readfile ninn2.dat
`p20
`s01
 @
 @if `p20 is &playernum then do
  @begin
@do write
`r0  `2One of your whores pulls your arm. Feel like some? [`0Y`2/`0N`2] :`x
@do readspecial `s01 YN
  @do goto PONWARD
  @end
 @end
@do write
`r0  `2A skanky looking woman pulls your arm.  Speak with her? [`0Y`2/`0N`2] :`x
@do readspecial `s01 YN

@label PONWARD
@if `s01 is N then do
  @begin
  @if `07 < 5 then do
    @begin
    ;person is evil
    @do saybar
You brutally kick her in the face.
    @closescript
    @end
  @do saybar
You push her away.  Poor lost soul.
   @closescript
  @end
@pauseoff
@routine showwhore
@if ninn2b.dat exist false then do
  @begin
  @writefile ninn2b.dat
25
0
  @end
@readfile ninn2b.dat
`p25
`p27
@
@show
`r0`c  `r1`% Close encounters with the whore kind  `r0

@if ninn2.dat exist true then do
 @begin
 @readfile ninn2.dat
`p20
`s01
 @
 @if `p20 is &playernum then do
  @begin
  @show
  `0"Oh, it's you, sir. Sorry. I didn't recognize you. Of course it's
  free for you, sir."

`k
  @do goto PSLEEP
  @end
  @end
  @show
`0  "Looking for a good time, sir?  Only `$$`p25`0." `2she drolls.

`2  Not a bad price, but her clothes are ragged and her face dirty... Would
  it be safe?

@do write
`x`x
@do `v01 is 1
@choice
Why not?  Daddy needs to relax a little bit.
Not today.
@if response is 1 then do
  @begin
  @if money < `p25 then do
    @begin
    @show
`\`\  `2You lack the funds for a whore right now.

`k
    @drawmap
    @update
    @closescript
    @end
  @do money - `p25
  @do `p27 + `p25
  @do delete ninn2b.dat
  @writefile ninn2b.dat
`p25
`p27
  @
   @writefile mail\mail`p20.dat
  `0Business note:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 has slept with one of your whores, you pimp daddy! 
  Cha-Ching!

  @label PSLEEP
  @do x is 54
  @do y is 12
  @do `p20 is random 5 1
  @if `p20 is 1 then do
    @begin
    ;ha ha, user caught vd
    @do `p12 is 1
    @end
  @do `p34 + 1
  @show
`\`\  `2She leads you to her bed.

`k`c  `r1  All Aboard  `r0

`2  You remove her garments - as you touch her skin flakes fall off in
  your hand.  You shudder as a finger brushes over a large open sore on her
  back.

`k  It is over quickly.  Your reverie is interrupted by the sound of wretching
  in a corner.  She seems sick.

  `4THERE IS A 20 PERCENT CHANCE THAT YOU JUST CAUGHT SOMETHING.

`k
  @drawmap
  @update
  @do saybar
You put your pants back on.
  @closescript
  @end

@show
`\`\  `$"I do not desire your services right now, woman," `2you inform
  her.

`k
@drawmap
@update

@#inn
;this is for the sea hag hotel
@if ninn2.dat exist false then do
  @begin
@do `v01 is 1
@offmap
@do moveback
@show
`r0`c  `%`r1  THE SEA HAG HOTEL  `r0

  `2This place looks a little campy - slack jaws sea harlots wander around
  the rooms aimlessly.

  A balding woman with a few teeth missing smiles warmly you at you.

  You are holding `$$&money`2.

@do write
`x`x
@choice
Get a room (`$$50`%)
I'd like to buy this place!
Leave
Make PortTown your home
@if responce is 4 then do
        @begin
        @do `p69 is 4
        @do write
`\ PortTown is now your home. `\`k
        @do goto inn
        @end
@if response is 1 then do
  @begin
  @if money < 50 then do
    @begin
    @show
`\`\`2  You are short on cash.  Pretty sad when you can't even afford to stay
  here...

`k
    @drawmap
    @update
    @closescript
    @end
   @show
`\`\  `2You give the woman the cash.  She leads you inside...
`\`k
   @do money - 50
   @do x is 24
   @do y is 4
   @do map is 153
   @loadmap 153
   @drawmap
   @update
   @closescript
  @end

@if response is 2 then do
 @begin
 @show
`\`\  `$"I'd like to buy this crummy place," `2you say to her.

`k  `0"And keep me out of a job!?! You're a despicable person. Out! Out!"

`k  `$"Whoa, hold up. I'd let you work for me."

`k`rc`c`\`\  `2She eyes you carefully. `0"Hrm. `%50,000`0, no less."

@do write
`x`x`x
@do `v01 is 1
@choice
>&money 49999 I'll take it!
That's a rip off, bitch
@if response is 1 then do
 @begin
  @if ninn2.dat exist true then do
    @begin
    @show
`\`\  `2Oh crap!  Someone bought it while you were thinking about it.  You
  will regret your snail like thinking for the rest of your days.`\`\`k
    @do goto done
    @end

  @writefile ninn2.dat
&playernum
NONE
0
0
0
0
    @do money - 50000
    @do addlog
  `0`n `2buys `%Sea Hag Hotel`2!
     @routine seperate in gametxt.ref

  @show
`r0`c  `r1 `%The Hag, your Employee and Friend `r0
`\`2  Nice job!  You now own her Inn!

  `2Enjoy your new home.

`k
@label POTIOND
@show
`c`\
  `2What would you like your toilet water to be made out of?

@do write
`x`x`x
@do `v01 is 1
@choice
Silver Potion
Gold Potion
Red Potion
What!?!
@if response is 4 then do
 @begin
 @show
`\`\  `2Let's explain, shall we...

  `0In your private room, there will be a toilet. In it, there will be water.
  What kind of water is your choice. This water will be available to drink
  once per day by yourself and whomever you allow inside your private room.

        `2Types of Water:

           `$-`0Silver Potion`2: Dodge goes up by 1!
           `$-`0Gold Potion`2: Muscle goes up by 2!
           `$-`0Red Potion`2: Max HP goes up by 5!

  `2That's what we're talking about...

`k

  @do goto POTIOND
  @end

@if response is 1 then do
 @begin
 @show
`\`\  `0"Okay," `2He says, and pours the contents of a Silver Potion into
  your toilet water. `0"I'll refill it once a day."

`k
 @if NINNW2.DAT exist true then delete NINNW2.DAT
 @writefile NINNW2.DAT
1
Silver Potion
 @end
@if response is 2 then do
 @begin
 @show
`\`\  `0"Okay," `2He says, and pours the contents of a Gold Potion into
  your toilet water. `0"I'll refill it once a day."

`k
 @if NINNW2.DAT exist true then delete NINNW2.DAT
 @writefile NINNW2.DAT
2
Gold Potion
 @end
@if response is 3 then do
 @begin
 @show
`\`\  `0"Okay," `2He says, and pours the contents of a Red Potion into
  your toilet water. `0"I'll refill it once a day."

`k
 @if NINNW2.DAT exist true then delete NINNW2.DAT
 @writefile NINNW2.DAT
5
Red Potion
 @end


@drawmap
@update
@closescript
  @end

@if response is 2 then do
 @begin
 @drawmap
 @update
 @closescript
 @end

 @end

@drawmap
@update
@closescript
@end

;---IF INN HAS BEEN PURCHASED
@do `v01 is 1
@offmap
@do moveback
@if ninn2.dat exist false then do
 @begin
 @do goto INN
 @end
@if ninn2.dat exist true then do
 @begin
@readfile ninn2.dat
`p20
`s01
@
@do `s02 is getname `p20
@do `p21 is deleted `p20

@if `p21 is 1 then do
  @begin
   ;owner got deleted!!!  Lets put it up for sale again.
   @do delete ninn2.dat
    @do addlog
  `%Sea Hag Hotel`2 is put on the market today.
     @routine seperate in gametxt.ref
  @end
 @if `p20 is &playernum then do
  @begin
   @do x is 24
   @do y is 4
   @do map is 153
   @loadmap 153
   @drawmap
   @update
   @do saybar
`!Hag: `$Welcome home...
   @closescript
   @end
 @show
`r0`c  `%`r1  THE SEA HAG HOTEL, owned and operated by `0`s02  `r0
 @end 
@show

  `2This place looks a little campy - slack jaws sea harlots wander around
  the rooms aimlessly.

  A balding woman with a few teeth missing smiles warmly you at you.

  You are holding `$$&money`2.

@do write
`x`x
@if ninn2a.dat exist false then do
  @begin
  @writefile ninn2a.dat
200
0
  @end
  @readfile ninn2a.dat
`p26
`p27
  @
@do `v01 is 1
@choice
Get a room (`p26 gold)
How's the owner?
Leave
 @if response is 1 then do
   @begin
   @if money < `p26 then do
    @begin
    @show
`\`\`2  You are short on cash.  Pretty sad when you can't even afford to stay
  here...

`k
    @do goto INN
    @end
   @do money - `p26
   @show
`\`\  `2The Hag takes your money and escorts you into the inn.
`k

  @do `p27 + `p26
  @do delete ninn2a.dat
  @writefile ninn2a.dat
`p26
`p27
  @
   @writefile mail\mail`p20.dat
  `0Business note:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `n`2 has stayed at your Inn!  Cha-CHING!

   @do x is 24
   @do y is 4
   @do map is 153
   @loadmap 153
   @drawmap
   @update
   @closescript
  @end

  @if response is 2 then do
    @begin
    @show
`\`\  `$"Listen, I've got a problem here with the management. `0`s02 `0is a
  no-good, lying sack of, for crying out loud, bum who charges too much
  and I can't afford to stay here."

`k  `2The Hag gasps at you. `0"How dare you? Of my manager! `0`s02 `0is a
  decent person! And, damn, is the toilet water good with 'em around!"

`k
  @do goto INN
  @end

@drawmap
@update


@#heal
@offmap
@do moveback
@do `v01 is 1
@show
`r0`c  `r1  PortTown Healing  `r0

`2  Hoss looks you over for wounds.

@do write
`x`x
@if `p02 < `p03 then do
 @begin
@do `p20 is `p03
@do `p20 - `p02
@do `p20 / 2


 @show
`0"You don't look so good, friend!  I'll fix ya up for `$`p20`0 gold, k?"

@do write
`x`x
@choice
Heal yourself for `$$`p20`%. (you have `$$&money`%)
Limp on out.
@show
`\
@if response is 1 then do begin
 @begin
 @if money < `p20 then do
  @begin
  @show
  `0"Sorry old s&boy, we don't do charity work here."

 @do write
`x`x
 @choice
Well Christ!  How am I supposed to make money when I can barely walk?
Jesus, how 'bout a little compassion for once in your life old man!
How 'bout some of your 'special' pills then?
Yes sir.
@show


@if response is 4 then do
 @begin
 @show
  `0"That's a good lad, now get along with you!" `2 he laughs.

`k
 @end

@if response is 1 then do
 @begin
 @show
  `0"Damn it, s&boy, be a s&man for once in your life!"`2 he yells.

`k
 @end
@if response is 2 then do
 @begin
 @show
  `0"I cannot respect a man who doesn't work," `2Hoss informs you.

`k
 @end
@if response is 3 then do
 @begin
 @do `p02 = 1
 @show
  `0"Sure.  Take uh.. these." `2 he snickers.

  `2You gulp down the weird black pills

  `4HIT POINTS GO DOWN TO 1.

`k
 @end

  @drawmap
  @update
  @closescript
  @end

@do money - `p20
@do `p21 = `p03
@do `p21 - `p02
@do `p02 = `p03
@show
  `2You feel much better as `0`p21`2 hitpoints are restored.

`k
  @end
  @drawmap
  @update
  @closescript
@end

@show
`0"You have no ailments, come back later!" `2Hoss laughs.

`k
@drawmap
@update


@#PIRATE
;The pirate ship
@DO MOVEBACK
@BUSY
@DO `v01 is 1
@SHOW
`r0`c  `r1 Look a Pirate ship!! `r0

  `2The guard yells `0"Arrr, I be a pirate, and you be on me ship.  What do
  you want you land lover?"
                      
@DO WRITE
`x`x
@CHOICE
Ask some pirate trivia
Can I become a pirate?
@IF RESPONSE IS 1 THEN DO
@Begin
@Show
`\`\  `$"What's another name for pirate treasure?"`2 you ask.
`\`k  `0"I believe that'd be booty!!!" `2he replies.
`\`k
@DRAWMAP
@UPDATE        
@CLOSESCRIPT
@End
@IF RESPONSE IS 2 THEN DO
@Begin
@Show
`\`\  `0"Arrr, I don't think ye be cool enough to join our drunken
   crew quite yet," `2the scurvy pirate replies.
`\`k
@DRAWMAP
@UPDATE
@CLOSESCRIPT
@End

@#CARGO
;The Cargo ship
@DO MOVEBACK
@BUSY
@DO `v01 is 1
@SHOW
`r0`c  `r1 Looks like a cargo ship `r0

  `2A deckman approaches you `0"We're headed to ArrisVille.  Do you have any
  thing that you need to be transported there?"
                      
@DO WRITE
`x`x
@CHOICE
Nope, not really
Stowaway on board
@IF RESPONSE IS 1 THEN DO
@Begin
@Show
`\`\  `$"Nope just taking a look around,"`2 you reply.
`\`k
@DRAWMAP
@UPDATE        
@CLOSESCRIPT
@End
@IF RESPONSE IS 2 THEN DO
  @Begin
  @if `p07 > -10 then do
   @begin
   ;they ain't so bad
  @Show
`\`\  `2You would never do such a thing!  Maybe if you were `b-10`2 alignment
  or less...

`k
  @DRAWMAP
  @UPDATE
  @CLOSESCRIPT
   @end

  @Show
`\`\  `$"Nothing much," `2you say.  He turns and walks away.  `$"Except for
   myself," `2you laugh as you sneak under deck.

@do `p20 random 80 20

@if `p18 > `p20 then do
  @begin
  @show
  `2The crew searches the ship before launch`l. `l. `l. `l. `l. `%YOU HID WELL!

`k  `2At Arrisville you pop out well rested.  Ahh, this is the life!

`k
  @do x is 66
  @do y is 19
  @do map is 444
  @loadmap &map
  @DRAWMAP
  @UPDATE
  @CLOSESCRIPT
  @end
  @do x is 28
  @do y is 7
  @do map is 155
  @loadmap &map
  @do `p02 / 2
  @show
  `2The crew searches the ship before launch`l. `l. `l. `l. `l. `bYOU HID BADLY!

`k  `2They find you!  `0"Lookie what we got here boys!  Let's teach this
  vagrant a lesson!"

`k  `2They beat the hell out of you and send you home.   Damn!

`k  `2Ack... Perhaps if you had more skill you would have a better chance...
  (aka, more quest points..)

`k
@DRAWMAP
@UPDATE
@CLOSESCRIPT
@End

@#TRAVEL
;The travel offices
@DO MOVEBACK
@BUSY
@DO `v01 is 1
@SHOW
`r0`c  `r1`% PortTown Travel `r0

  `0"Welcome to PortTown travel, how may I help you?" `2Asks a receptionist.            
@DO WRITE
`x`x
@CHOICE
Purchase a world map
Buy a ticket for somewhere
What happened to the healers?
Leave
@IF RESPONSE IS 3 THEN DO
@begin
@SHOW
`r0`c  `r1`% PortTown Travel `r0

  `0"I heard that someone got upset with the healers because the healers would
  not heal the arsonist for free. So the angry guy left, that night the place
  got torched. Go figure."
`\`k
`\`\  `$"Sorry wrong building,"`2 you say as you saunter out the door.
`\`k
@DRAWMAP
@UPDATE        
@CLOSESCRIPT
@end
@IF RESPONSE IS 1 THEN DO
@Begin
@Show
`\`\  `$"I'd like to get a map,"`2 you reply.
`\`k  `0"Okay that'll be 20 gold,"`2 she states.

@if money < 20 then do
        @BEGIN
        @show
  `2You don't have that much, so naturally you take it out on her.

`k  `$"Look you stupid wench, since when do maps cost that much?!?! You just
  lost a customer, lady!" `2you scream.

`k  `2You see tears in her eyes as you storm out the door.
`\`k
        @DRAWMAP
        @UPDATE
        @CLOSESCRIPT
        @End
@show
`2  You plop down the money.`\`\`k
@do money - 20
@do `i20 + 1
@DRAWMAP
@UPDATE        
@CLOSESCRIPT
@End

@IF RESPONSE IS 2 THEN DO
@Begin
@Show
`\`\  `$"I need to get out of town," `2you say.  She smiles, `0"Okay where
   would you like to go?" `2You review your choices.

`k
@show
`r0`c                         `r1 `%Porttown Booking Center


@buymanager
26
27
33
@do goto TRAVEL
@End


@IF RESPONSE IS 4 THEN DO
@Begin
@Show
`\`\  `$"Sorry wrong building,"`2 you say as you saunter out the door.
`\`k
@DRAWMAP
@UPDATE        
@CLOSESCRIPT
@End

@#MART
;The quicke mart (tm) 
@DO MOVEBACK
@BUSY
@DO `v01 is 1
@label martstart
@SHOW
`r0`c  `r1`% Wow, they have everything in here `r0

  `2The clerk behind the counter greets you `0"Oh my golly-gosh, customers!!
  How ever my I help you?"
                      
@DO WRITE
`x`x
@CHOICE
Ask for some beef-jerky
Get a super squishie for 1 gold
Check out aisle five for supplies
Leave
@if responce is 4 then do
 @begin
 @drawmap
 @update
 @closescript
 @end

@IF RESPONSE IS 3 THEN DO
@Begin
@Show
`\`\  `$"I need to get some new gear," `2you say.  He smiles, `0"Okie Doaky
  I hope you enjoy our fine selection of death tools."  `2You review your
  choices.

`k
@show
`r0`c                         `r1 `%Quick-E Mart Stuff


@buymanager
35
36
37
40
@do goto martstart
@End

@IF RESPONSE IS 1 THEN DO
@Begin
@Show
`\`\  `2You take a look at the jerky, you're pretty sure you're better off
  eating something you killed outside.`%
`\`k
@DO goto martstart
@End
@IF RESPONSE IS 2 THEN DO
@Begin
@if money < 1 then do
  @begin
  @show
`\`\  `0"Are trying to be ripping me off, s&sir?!  You shall not taste a single
  drop of my wonderful liquid!"

`k
  @do goto martstart
  @end
@do money - 1
@Show
`\`\  `$"Give me a super squishie, one that's made entirely out of syrup," `2you
  command.
  `0"An all syrup super squishie?  Oh such a thing has never been done," `2the
  clerk gasps.
`\`k  `$"Just make it happen," `2you say as you lay down the dough.
  `0"Oh dear," `2he moans and turns towards the machine.  It begins to
  rumble, you feel the walls shake.  `0"Oh no, she's breaking up," `2he
  yells....
`\`k  `5DING!
`\`k  `2The clerk turns and hands you your squishie.  `0"All done, if you
  survive, please come again."
`\`k
@DRAWMAP
@UPDATE
@CLOSESCRIPT
@End

@#bank
@do `p30 is 3
@do `v01 is 1
@offmap
@do moveback
@do `s09 is porttown
@routine DONEQUEST in VAULTS.REF
@label bankstart
@show
`r0`c  `r1 `%2nd Bank Of PortTown `r0

`2  A young clerk eyes you nervously.

@if bank more 0 then do
 @begin
 @show
`2  You are holding `$$&money`2, your account is at `$$&bank`2.

 @end

@if bank is 0 then do
 @begin
 @show
`2  You currently don't have an account with them.

 @end

@do write
`x`x
@choice
>&bank 0 Make a deposit.
>&bank 0 Make a withdrawal.
=&bank 0 Apply for an account here.
Leave
@if response is 3 then do
  @begin
  @do `v01 is 1
  @show


  `2The nervous boy asks you to sit down.

  `0"Your full name s&sir?"
  @do `s01 is NIL
  @do write
`x`x
  @do readstring 30 `s01
  @show


  `0"Your current residence?"

  @do write
`x`x
  @do readstring 50 `s01

  @show


  `0"Your net income yearly?"

  @do write
`x`x
  @do readnum 10
  @if `v40 less 20 then do
  @begin
  @show

  @end

  @if `v40 more 19 then do
   @begin
   @show


  `2You giggle to yourself `0-`2 how will they know you just totally lied on
  that last questions?!??  You only made `b19`2 gold pieces last year!
  @end
  @label bankagain
  @show

  `0"How much would you like to open your account with?" `2(you have `$$&money`2)

  @do write
`x`x
  @do readnum 10 money
  @if `v40 is 0 then do
   @begin
   @show


  `2You decide you really don't want to open an account with this
  branch.

`k
  @do goto bankstart
  @end

  @if `v40 > money then do
   @begin
   @show


`2  You don't have this much!  Try again.
   @do goto bankagain
   @end

  @if `v40 < 50 then do
  @begin
  @if money < 50 then do
   @begin
  @show

  `2The clerk eyes your shabby clothing distastefully - as if he knew
  this part was coming.

  `0"I'm sorry s&sir, we do require `$$50 `2to open an account.  And could
  you please leave the pen on your way out." `2he asks haughtily.

`k  `2You carefully return the pen.  `$"I'll be back someday.  And you WILL
  open an account for me then," `2you vow seriously.

  `0"Of course you will, s&sir.  Please exit now, remember, the sign does
  say no loitering.  And this means you." `2he says as he points to the
  door.

`k
@drawmap
@update
@closescript
   @end

  @show


  `0"S&Sir, `$$50 `0is required to open a new account!"
  @do goto bankagain
  @end
  @routine deposit in vaults.ref
  @do money - `v40
  @do bank + `v40
  @show


  `0"Very good S&sir!  Your new account has been opened!"

`k
  @do goto bank
  @end


@if response is 1 then do
  @begin
  @do write
`\
  @label deposit
  @do write
`\  `0"Deposit how much, s&sir?"`\`\`x`x
  @do readnum 10 money
  @if `v40 more money then do
  @begin
  @show
`\`\  `2Maybe you left the money in your other pants?  'cuz it sure ain't here.
  @do goto deposit
  @end
  @if `v40 less 0 then do
  @begin
  @show
`\`\  `2No, this isn't happening and you aren't here.
  @do goto withdraw
  @end
  @routine deposit in vaults.ref
  @do money - `v40
  @do bank + `v40
  @do goto bankstart

  @end

@if response is 2 then do
  @begin
  @do write
`\
  @label withdraw
  @do write
`\  `0"Withdraw how much, s&sir?"`\`\`x`x
  @do `p21 is bank
  @do `p21 - 1
  @if `p21 < 0 then `p21 is 0
  @do readnum 10 `p21
  @if `v40 more bank then do
  @begin
  @show
`\`\  `2You kind of don't have that much in your account.  Nice bookeeping!
  @do goto withdraw
  @end

  @if `v40 less 0 then do
  @begin
  @show
`\`\  `2No, I don't think so.
  @do goto withdraw
  @end

  @if `v40 is bank then do
   @begin
   @show


  `0"But s&sir!  This will close your account with us, are you sure you wish
  to do that?" `2the clerk chokes.

  @do write
`x`x
  @do `v01 is 1
  @choice
Ok, nevermind then.
Yeah.  Get my damn money.
@if response is 1 then goto bank

   @end
  @routine widthdraw in vaults.ref
  @do money + `v40
  @do bank - `v40
  @do goto bankstart
  @end



@drawmap
@update


@#HANG
;For sitting at the benches for public execution
@DO SAYBAR
`2You'd sit to watch an execution but there isn't one today...
                                                              
@#NOTDONE
@DO SAYBAR
`2Area unfinished.  Check back when beta ... 7 is ready.
@do moveback

@#FISHDOCK
;Fishing dock...
@do moveback
@if `i28 < 1 then do
  @begin
  ;person doesn't have fishing pole
  @DO SAYBAR
`2You doubt you would catch anything with your s&weapon...
  @closescript
  @end


@if `t10 < 1 then do
  @begin
  @do saybar
You have caught all the fish in the stream.  Maybe tomorrow.
  @closescript
  @end

@busy
@do write
`r1
@clearblock 21 21
@do move 3 21
@do write
`r0  `2Wanna try your luck fishing? [`0Y`2/`0N`2] :`x
@do readspecial `s01 YN

@if `s01 is N then do
  @begin
  @do saybar
Maybe later when the fish are biting.
  @closescript
  @end

@label fishcast

@do `p20 is `i29
@do `p20 + `i30
@do `s01 is `p20
@if `s01 is 0 then `s01 is no

@displayfile fishy.ans nopause
@Do move 55 1
@do write
`r4  `%The Fishing Dock  `r0
@Do move 56 2
@do write
You have `s01 fish.

@do `v01 is 1
@if `p11 < 11 then do
        @begin
        @do move 2 20
        @do write
`x You are exhausted for today, not enough turns.
        @do move 38 1
        @key
        @closescript
        @end
@do move 69 18
@choice
Cast far
Cast short
leave
@if responce is 3 then do
  @begin
  @drawmap
  @update
  @closescript
  @end

@if response is 1 then do
  @begin
  @do `p20 random 80 20
  @displayfile fishyl.ans nopause
  @do move 2 20
  @do write
`2You whip your rod fast and watch as your hook plops down
  @do move 2 21
  @do write
  about `0`p20`2 feet away.

@end

@if response is 2 then do
  @begin
  @do `p20 random 20 1
  @displayfile fishys.ans nopause
  @do move 2 20
  @do write
`2You gently cast the pole.  Your hook makes a soft splash
  @do move 2 21
  @do write
  about `0`p20 `2feet away.
        @do move 38 1
        @key
  
@end


  @do move 2 22
@do write
`2You wait.`l.`l.`l.`l.`l.                                   `%
        @do move 38 1
        @key
  
@if `t10 < 1 then do
@do `p20 random 15 1
  @do `p11 - `p20
  @begin
  @do move 2 20
  @do write
`4No fish are biting.`2                                      `%
  @do move 2 21
  @do write
`4The action is finished for today                            `%
        @do move 38 1
        @key
        @do move 38 1
        @key
  
  @do goto fishcast
  @end
@do `p20 random 2 1
@if `p20 is 1 then do
   @begin
  @do move 2 20
  @do write
`4Nothing happens.
  @do move 2 21
  @do write
`2You reel the line back in.   `%
        @do move 38 1
        @key
   @do goto fishcast
   @end
@if `p20 is 2 then do
   @begin
  @do move 62 14
  @do write
`%You have a bite!`2
        @do move 38 1
        @key

@do `p20 random 4 1
@IF `P20 IS 1 then do
  @begin
  ;catching a special fish
  @do move 2 22
  @do write
The fish is suprisingly strong!`%
        @do move 38 1
        @key
  
  @do move 2 23
  @do write
But you reel it in slowly...   `%
        @do move 38 1
        @key

@do `p20 random 4 1
@IF `P20 IS 1 then do
   @begin
  @do move 2 22
@do write
`bTHE LINE BREAKS!             `%
@do move 2 23
@do write
`2The mighty fish has escaped. `%
        @do move 38 1
        @key

   @do goto fishcast
   @end
        @do move 38 1
        @key
  
  @do move 2 23
  @do write
`2You have just caught a `0Sparkling Green Salmon`2!
  @routine removeline
  @do move 46 4
  @do write
`!/\
  @do move 46 5
  @do write
`!±±
  @do move 46 6
  @do write
`!/\
        @do move 38 1
        @key

  @do `i30 + 1
  @do `t10 - 1
  @do goto fishcast
  @end

  @do move 2 22
  @do write
  You slowly reel the fish to shore.
        @do move 38 1
        @key
  
  @do move 2 23
  @do write
  You have caught a Salmon.
  @routine removeline
  @do move 46 4
  @do write
`2/\
  @do move 46 5
  @do write
`2±±
  @do move 46 6
  @do write
`2/\
        @do move 38 1
        @key

   @do `i29 + 1
   @do `t10 - 1
   @do goto fishcast
   @end
  @end
@#REMOVELINE
  @do move 47 4
  @do write
`x`x
  @do move 47 5
  @do write
`x
  @do move 47 6
  @do write
`x
  @do move 47 7
  @do write
`x
  @do move 47 8
  @do write
`x
  @do move 47 9
  @do write
`x
  @do move 46 10
  @do write
`x`x
  @do move 49 5
  @do write
`x`x
  @do move 51 6
  @do write
`x`x
  @do move 53 7
  @do write
`3--
  @do move 47 7
  @do write
`3-
  @do move 55 8
  @do write
`x`x
  @do move 57 9
  @do write
`x`x`x
  @do move 58 10
  @do write
`x`x

@#WATERFALL
;Looking at the waterfall.
@DO SAYBAR
`2How nice!
@do moveback

@#WATERSIGN
;Sign at the water fall.
@DO SAYBAR
`2To waterfall observation dock.
@do moveback

@#TREES
;At the trees in the passage by the town
@DO SAYBAR
`2A spooky patch of autumn trees

@#shrine
@do `v01 is 1
@do moveback
@busy
@show
`r0`c  `r1`%  THE SHRINE OF BABOS  `r0`\
`2  You kneel in front of the shrine and meditate.`\
@

@if `v04 > 4 then do
  @begin
  @show
`k  Nothing happens.`\`\`k
  @drawmap
  @update
  @closescript
  @end

@if `v04 is 0 then do
  @begin
  @show
`k  Nothing happens.  Maybe if you came on the right day...`\`\`k
  @drawmap
  @update
  @closescript
  @end



@if `t09 is 1 then do
  @begin
  @show
`k  Nothing happens.  You believe there may be limits to even a God's kindness.`\`\`k
  @drawmap
  @update
  @closescript
  @end


;`v04: Lucky Shrine?: 1 = Money, 2, hitpoint, 3, strength 4 defence, else

@if `v04 is 1 then do
  @begin
  @show
`k  `$$500 `0magically appears in your pocket!`\`\`k
  @do money + 500
  @do `t09 is 1 
  @end

@if `v04 is 2 then do
  @begin
  @show
`k  `0You feel healthier!  You get two more hitpoints!`\`\`k
  @do `p03 + 2
  @do `p02 + 2
  @do `t09 is 1 
  @end

@if `v04 is 3 then do
  @begin
  @show
`k  `%Belar`0 makes you stronger.  You gain one muscle point!`\`\`k
  @do `p04 + 1
  @do `t09 is 1 
  @end

@if `v04 is 4 then do
  @begin
  @show
`k  `0You feel the gods protecting you.  You gain one dodge point!`\`\`k
  @do `p05 + 1
  @do `t09 is 1 
  @end

  @drawmap
  @update
  @closescript

@#DONE
 @drawmap
 @update
 @closescript
 @end

@#nomore

