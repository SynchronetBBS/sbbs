This one of the main .ref files the LORD II engine will use as
references.  For help on making your OWN, check out REFHELP.DOC.

Default LORD II files use the vars like this:

The gold and bank vars are special cases, other vars do not have names.

The list of how the variables are used has moved to VARLIST.DAT.  This file
is read by the configuration program - it gives descriptions in the player
editor from this file.  Check this file if you want to know what a var is
for, or what free ones are available.


@#showlog
@routine readlog in logstuff.ref
@drawmap
@update

@#busy
;player cannot play, someone is attacking him or something
@show
`c  `r1                             `%PROBLEM.                                 `r0

  `2Someone is attacking/giving you an item, please try again in few
  minutes.

`k
@halt 0

@#FULL
;There are already 200 people playing.
@show
`r0`c  `r1`% UH OH. `r0

`2  This game already has `0200`2 people in it.  Please try again in a few
  days, people are deleted after `%15`2 days of inactivity.

  `2Sorry.  Until then, you can only dream of the wonderful world that awaits.

`k
@

@#drawstats
@do move 1 5
@do `s10 is `p01
@do pad `s10 18
@do write
  `2Experience  : `0`s10  `2Hitpoints    : (`0`p02`2 of `0`p03`2)`\
@do `p20 is gold
@do `s10 is `p20
@do pad `s10 18
@do `p20 is bank
@do `s09 is `p20
@do write
  `2Gold on hand: `$`s10  `2Gold in bank : `$`s09`\
@do `s10 is `0
@if `p07 less 0 then `s10 is `4
@do `s10 add `p07
@do pad `s10 18
@do write
  `2Alignment   : `0`s10  `2Gems         : `0`p19`\
@do `s10 is `p11
@if `p11 is 0 then `s10 is `4None
@if `v05 < 1 then `s10 is Unlimited
@do pad `s10 18
@do write
  `2Turns Left  : `0`s10  `2Time left    : `0&timeleft`\

@do `s10 is S&weapon
@do pad `s10 18
@do `p20 is S&wep_num
@do `p20 + `p04 (adding muscle to weapon strength to show it)
@do write
  `2Weapon      : `0`s10  `2Total Attack : `0`p20    `\
@do `s10 is S&armour
@do pad `s10 18
@do `p20 is S&arm_num
@do `p20 + `p05 (adding dodge to armour defense to show it)
@do write
  `2Armour      : `0`s10  `2Total Defense: `0`p20     `\

@do `s10 is `p08
@do pad `s10 18
@do write
  `2Charm       : `0`s10  `2Quest Points : `0`p18`\


@#drawwep
;code to redraw the weapon for arming/unarming.
@do move 3 9
@do `s10 is S&weapon
@do pad `s10 18
@do `p20 is S&wep_num
@do `p20 + `p04 (adding muscle to weapon strength to show it)
@do write
`r0`2Weapon      : `0`s10  `2Total Attack : `0`p20  `\

@#drawarm
;code to redraw the armour for arming/unarming.
@do move 3 10
@do `s10 is S&armour
@do pad `s10 18
@do `p20 is S&arm_num
@do `p20 + `p05 (adding dodge to armour defense to show it)
@do write
`r0`2Armour      : `0`s10  `2Total Defense: `0`p20  `\

@#stats
@show
`r0`c`0  `n`2's status and information. (level `0`p09`2)
`0-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
@routine drawstats in gametxt.ref
@do move 1 12
@show
`r5  `%Item                                  Quantity                               `r0
@do move 1 24 
@do write
`r5`%  `$`%Q `$to quit, `%ENTER`$ to use/equip/eat, `%D`$ to drop, `%N`$ for next page & `%P`$ for prev.  `r0
@do move 1 12
@#closestats
@drawmap
@update
@closescript

@#startgame
;do startgame routines here
@version 99
;if version 99 or newer isn't detected, it will tell the player to
;upgrade
@do `v01 is 1
@if map < 2 then map is 155
@if map > 1600 then map is 155

@do write
`r0`c

@if `p10 is &time then do
 @begin
 ;guy has already played today

@if dead is 1 then do
 @begin
  @if mail\mail&playernum.dat exist true then do
   @begin
   @checkmail
   @key
   @end

  @if `v03 is 0 then do
   @begin
   @do `v01 is 1
   @label deadstart
    @show
`r0`c  `r1 `%A PROBLEM. `r0

`2  You are lying battered and bruised on the ground.  You can't do much
  in this state.

  `0Try again tomorrow.

@do write
`x`x
@choice
See todays news
List players
`4Quit back to the BBS
@if responce is 1 then do
  @begin
  @routine readlog in logstuff.ref
  @do goto deadstart
  @end
@if responce is 2 then do
  @begin
@do write
`r0`c  `0Ranking players...

@do delete temp`*.dat         ;`* is node number
@writefile temp`*.dat
`r0`c`r1                             `%  LORD II RANKINGS                            `r0
                
  `0Name                     Experience  Level   Status Alignment Quests
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
@
@lordrank temp`*.dat 1
@displayfile temp`*.dat
@key bottom
  @do goto deadstart
  @end

@show
`\`\  `2Bye.

       @halt
      @end
     @end


  @show
  `r1 `% DAY &time  `r0

`2  You have already been out and about today.

 @if `p11 > 0 then do
  @begin
  @show
  `2You have `0`p11`2 turns left today.

  @end
 @end

@if `p10 not &time then do
  @begin
  ;NEWDAY!
  @do `p10 is &time
  @do `p11 is `v05 ;this will give them the turns set in rules.ref
  @do `t04 is 0 ;<-- Can fight master now
  @do `p02 is `p03 ;<-- Give 'em max hit points
  @do dead is 0 ;<-- Is now alive
  @do `t09 is 0 ;<-- Has not visited shrine today
  @do `t10 random 10 5 ;<-- Amount of fish in stream today
  @do `p31 is `v07 ;<-- Reset quests per day possible 
  @do `p33 is `v08 ;<-- Reset gambles per day possible 
  @do `p32 + 1 ;<-- Advance quest age.. doesn't matter if there is one active
  @bitset `t12 7 0 ;<-- Has not LOST at Arena yet today
  @if s&wep_num > 0 then do
  @begin
  @show
  `r1 `% DAY &time  `r0

 @if `p11 > 0 then do
  @begin
  @show
  `2It is morning.  You have `0`p11`2 turns left today.

  @end
  @show
`2  You wake up early, strap your `0s&weapon`2 to your back and prepare for
  adventure!

  @end

  @if dead is 0 then do
   @begin

    @if s&wep_num is 0 then do
    @begin
    @show
  `r1`%  DAY &time  `r0

 @if `p11 > 0 then do
  @begin
  @show
  `2It is morning.  You have `0`p11`2 turns left today.

  @end
@show
`2  You wake up early, and prepare for adventure!

     @end
    @end


  @if `p12 is 1 then do
  @begin
  @show
`k  `%You feel extra weak this morning.

  `2You have `4VD`2.  You must find a place to cure this soon, or you may die.

  @do `p02 / 2
  @end

  @if `v05 > 0 then do
    @begin
      @if `i57 > 0 then do
        @begin
        @do `p20 is `i57
        @do `p20 * 500
        @do `p11 + `p20
        @show
  `2You get `0`p20`2 extra turns from `%Moonstone`2 power!

    @end
  

  @end



@if `v03 is 1 then do
 @begin
 @if dead is 1 then do
 ;if he is dead when he logs on
  @begin
   @do X is 27
   @do Y is 7
   @do dead is 0 ;he is now alive
   @do `p02 is `p03 (give 'em max hit points)
   @do MAP is 155 (move 'em home)
  @end
 @end


@if map < 0 then map is 155
@if map is 1 then do
  @begin
  @do map is 155
  @show
  `2Your mother is sitting by your bed, weeping.

`k  `$"Mom?  Why the tears?  Is it something I've done?" `2you ask.

`k  `0"I was so worried!" `2you mother sobs, `0"When the town elders
  brought you here last night I was sure you were dead!"

  `$"I'm sorry mother.  I shall never scare you like this again!" `2you
  promise.

  @end
 @end

@if x < 1 then x is 27
@if y < 1 then y is 7
@if x > 80 then x is 27
@if y > 24 then y is 7

@show
`k
@do talk `0`n`x`2has`xentered`xthe`xrealm. all

@routine readlog in logstuff.ref


;do end routines here
@#endgame
@do talk `0`n`x`2has`xleft. all
@if dead is 0 then do
 @begin
 @do move 1 24
 @show

`r0  `2You are lulled to sleep by a gentle breeze...

  `0Come back soon.
`2
 @end

@#RUN ;if the guy runs
@drawmap
@update

@#DIE
@do move 3 24
@do write
`r0                                                                           `x

  @do X is 27
  @do Y is 7
  @do money / 2
  @do `p02 is `p03 (give 'em max hit points)
  @do `p20 is `p01
  @do `p20 / 5
  @do `p01 - `p20
  @do MAP is 155
  @do dead is 1
  @update_update

  @do talk `4Newsflash:`0`x`n`x`2was`xdefeated`xby`x`0`e`2! all
  @do addlog
  `0`n`2 was whipped up on by `0`e`2!
  @routine seperate
@do move 3 24
@do write
`4You've been killed. `k

@pauseoff
@show
`r0`c
[34C[0m‹‹‹€€€€€€€€€‹‹‹
[29C‹€€[1;30;47m∞∞∞∞[0;47m€[1;30m∞[0;47m€€€€€€€€€€€[40m‹
[28C€[1;30;47m∞∞∞∞[0;47m€€€€€€€€€€€€€€€€€€[40m›
[27C€[1;30;47m∞±±∞[0;47m€€€€€€€€[1;30mﬁ‹[0;47m€€€€€€€€€[40m€
[19C‹‹[5Cﬁ[1;30;47m∞±≤∞[0;47m€€€€€€€€€€[1;30m€€ﬂﬂ[0;47m€€€€€[40m
[A[50C[47m[1;30mﬁ[0m€€
[18Cﬂ‹€›    [1;30;47m∞±≤±∞[0;47m€€€€€[1;30m‹ ›[0;47m€[1;30mﬁ€‹[40m
[A[43C[0;47m€€€€€[1;30m‹‹≤[0m€[47m€[1C[40m‹[1;47m±≤[40m‹
[17C[30m‹[47m∞[0;47m€€[1m‹[0m‹   [1;30;47m±≤±∞ [0;47m€€[1;30m∞[40m
[A[34C[0;47m€[1;30m∞∞≤›‹ﬂ∞‹[0;47m€[1;30m∞[0;47m€€€€[1;30mﬂ≤›[40m
[A[52C[47m[0m›[1;30m‹[47m∞[0;47m€[1m±[40m›
[17C[30;47m≤±∞[0;47m€€[1mﬂ‹[0m‹ [1;30mﬁ[0m‹ﬂﬂ    [1;30mﬂﬂ≤[47m[40m
[A[37C[47m≤≤∞‹[40m≤ﬂﬂ[5C[0mﬂﬂ[1;30;47mﬂ[40mﬁ[47m±∞[0mﬂ ﬂ
[18C[1;30mﬂﬂﬂ[47m∞∞[0;47m€ [1;30m∞[1C[0m‹    [31mﬂ   [1;30m±≤
[A[38C[47m≤≤∞[40m±   [0;31mﬂ    [37mﬁ›[1;30;47m≤[40mﬂ
[23C[0mﬂ[1;30;47m∞±[40m›[0mﬁ€‹‹    ‹‹[1;30;47mﬂ[0mﬂﬂ[1;30mﬂ
[A[41C[47mﬂ[0m‹‹    ‹€[1;47m‹[0m›[1;30m›
[25C[47m≤[40mﬂ[0m‹[1;30;47m‹ [0;47m€€[1;30mﬂﬂ‹‹ﬂ[0m› [1;30m› [0mﬁ
[A[42C[1;30;47m‹‹ﬂﬂ[0;47m€€€€[1mﬂ‹[0m‹
[26C€[1;30;47mﬂ[0;47m€€€€[1;30m‹≤≤‹[0;47m€[2C[1;30;40m≤‹ [47m∞±[40m
[A[44C[47m∞[0;47m€€€€€[40mﬂﬂ
[27Cﬂ€€[1;30;47m∞≤[40m≤±≤[47m∞[0m‹‹[1;30;47m∞≤±∞±[40m≤ﬂ[0mﬂ ﬂﬂﬂﬂ
[29C[1;30m‹‹ ﬂ‹ [0mﬂﬂ[1;30mﬂﬂ ﬂ[0mﬂ[1;30mﬂ ‹ﬂ [47m≤±[0m‹
[27C‹[1;30;47m∞±≤[40m› ﬂ [47m∞[0m›[1;47m≤€[40mﬁ€[0mﬂ‹[1;30;47m[40m
[A[43C[47m∞[40mﬂ ‹ [47m±∞[0;47m€[40m‹
[22C[1m‹  [47m±∞[30m∞∞[40mﬂ [0m‹ [1;30mﬂ [0m‹ ‹[1m‹ ‹[0m‹ ‹ 
[A[45C[1;30mﬂ‹[47m∞[1C[0mﬂ[47m  [1m±[40m‹‹‹
[19C‹[47m≤≤±∞∞[30m±[0m›ﬂ ‹€€[1;30;47mﬁ[0;47m€[40m‹‹ ‹‹‹‹‹ ‹‹
[A[45C[1;30;47m∞±[0;47m€€[40m‹ ﬂ[1;30;47m±[37m∞±[40mﬂ[47m≤[40m
[19C[0;31m‹ [37m‹ﬂ€[1;30;47m±[40mﬂ[0;33m‹≤‹‹ [37mﬂ[1;47mﬂ‹[30m[40m
[A[34C[47mﬂ[0;47m€€€[1;30m‹ﬂ[0;47m€€€€ [1m‹ﬂ[0mﬂ [33m‹‹›[1;30mﬁ
[A[53C[47m±[0m‹›[31m‹
[17C∞±≤€‹[37mﬂ[1;30mﬂ[0;31m‹‹‹[33;41mﬂﬂﬂﬂ[40m‹ [37mﬂ[1;47mﬂ[40m
[A[35C[0;47m€€[1;30m‹±[40m≤[47m±‹[40mﬂ[47m‹[37mﬂ[0mﬂ [31m‹
[A[48C[33;41mﬂﬂﬂ[31;40m‹‹[1;30mﬂ[0;31m‹‹€≤±∞

                     `%You have made an error, s&boy.

`k
 @if `v03 is 0 then do
   @begin
 ;rules.ref file says unlimited deaths not allowed, so we will boot them
 ;till tomorrow

 ;take them off the map
  @do map is 1
   @halt
   @end

 @pauseon

 @do dead is 0

 @if `p07 less 0 then do
 @begin
 @show
`r0`c`%  `r1IT FEELS KINDA WARM IN HERE?`r0

  `4You open your eyes.

`k  `4There are naked screaming people all around you.  A little boy with
  a ghastly face begins to gnaw on your leg.

`k  `6"Din, leave the newcomer alone.  S&he isn't staying." `4a slow voice that
  can only be described as an abomination dolefully states.

`k  `2You feel yourself floating away... faster and faster.
 @end

 @if `p07 more -1 then do
 @begin
 @show
`r0`c`%  `r1APPARENTLY DEATH ISN'T THE END.`r0

  `2You open your eyes.

`k  `2You are lying on a table of solid rock in a marble room.  Hundreds of
  soft voices are murmuring all around you, but you see no one.

`k  `%"Send s&him back." `2a voice whispers, this one louder than the others.

`k  `2You feel yourself floating away... faster and faster.

`k
 @end


@show
`c  `r1`%AWAKENING.`r0

  `0"Can you hear me, `n?" `2a stressed voice shouts.

  `$"Yeah.  Christ mom, lemmie up, I'm fine." `2you assure your mother.

  `0"You're alive!! The elders brought your body here yesterday, you must
  have fainted out there." `2your mother shouts, tears of joy streaming down
  her wrinkled face.

  `$"Ok, ok - nice little happy reunion and all that, but I gotta hit the
  road now. Buhbye." `2you tell her.  Afterall, you need to make the most of
  this new lease on life.

  `2You lose `4half`2 the gold you were carrying and `4`p20`2 experience.

`k
  @loadmap map
  @drawmap
  @update

@#iwon
;is run after someone kills a player - if in multinode combat, L2
;calls it directly, otherwise @#live calls it with a @routine.
;the enemy guy they just killed is automatically put in `v39.
  @if bounty.`v39 exist true then do
    @begin
    @readfile bounty.`v39
`p23
`p24
   @do money + `p24
   @do delete bounty.`v39
   @do quebar
`0BONUS: `%You collect a `$$`p24 `%bounty for this kill!
  @do addlog
  `0`n`2 collects a `$$`p24 `2bounty for the kill!
@writefile lognow.txt
                                 `2-`0=`2-`%=`2-`0=`2-
   @if `p23 not &playernum then do
     @begin
   @writefile mail\mail`p23.dat
  `0`n `2reports:
`2-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-
`0  `e`2 has been `4killed`2.  Good doing business with you.

     @end

 @if `p23 is &playernum then do
    @begin
   @do quebar
Haha, you got back your own money!  Nice.
    @end

   @end


@#live
@do talk `4Newsflash:`0`x`n`x`2has`xslain`x`0`e`2! all
@do `V40 random 10 1
@if `v40 is 1 then addlog
  `0`n`2 has killed `0`e`2 over a dispute involving cattle!
@if `v40 is 2 then addlog
  `0`n`2 has crushed `0`e`2's skull.
@if `v40 is 3 then addlog
  `0`n`2 has beaten `0`e`2 in a fair battle!
@if `v40 is 4 then addlog
  `0`n`2 has killed `0`e`2 to avenge his mother!
@if `v40 is 5 then addlog
  `0`n`2 buries `0`e`2's rotting body.

@if `v40 is 6 then addlog
  `0`n`2 thanks `4Belar`2 for the power to destroy `0`e`2.

@if `v40 is 7 then addlog
  `0`n`2 kills `0`e`2 for trespassing on s&his land.

@if `v40 is 8 then addlog
  `0`n`2 deprives `0`e`2 of living another minute.
@if `v40 is 9 then addlog
  `0`n`2 makes quick work of `0`e`2.
@if `v40 is 10 then addlog
  `0`n`2 slays `0`e`2.


@routine seperate
@run iwon
@#newplayer

@routine help in help.ref

@do `V40 random 5 1
@if `v40 is 1 then `s10 is Elwin
@if `v40 is 2 then `s10 is Geengish
@if `v40 is 3 then `s10 is Dimdom
@if `v40 is 4 then `s10 is Magi
@if `v40 is 5 then `s10 is Whinny


@show
`c  `%IN THE BEGINNING.

  `2There once was a poor `$`s10 `2couple who lived in a small village called
  `0Stonebrook`2.

  One day the mother felt very sick.  Scared, her husband took her to Nathan,
  the town physician - after careful examination he smiled wide.

  `0"Dear woman, you aren't sick - you are with child!"

@KEY
@show
`c  `%IN THE BEGINNING.

  `2The baby was born six months later - strangely enough on `0Gryphon Moon`2 day.

  The young mother was delighted to find she had a new baby
@do `v01 is 1
@do move 61 7
@choice
boy.
`#girl.
@if responce is 1 then sexmale is 1
@if responce is 2 then sexmale is 0
@do write
`\ 
@label namebaby
@do `v01 is 1
@if sexmale is 1 then `s10 is him`x
@if sexmale is 0 then `s10 is her`x
@do write
`\
@do write
  `2The mother decided to name `s10
  @do readstring 20 NIL
  @do stripbad `s10
  ; change dark blue to light blue in their name
  @do replaceall `1 `! `s10
  @do `p20 is length `s10
  @if `p20 less 1 then do
    @begin
    @do move 35 y
    @show
`bNothing`2.

  `2She decided to name the baby nothing `8-`2 instead she sold the child
  for `$20`2 gold pieces.  That night she was overcome with guilt and hung
  herself on the rotting pine behind the cottage.

  Have a nice day, loser!

    @closescript
    @end



  @if `p20 less 3 then do
    @begin
    @show
 
 
  `2But the husband disagreed.  `0"Not such a short name, dear!"
    @do goto namebaby
    @end

  @if checkdupe `s10 true then do
    @begin
    @show


  `2But the husband disagreed.  `0"We already know someone with that name!"
    @do goto namebaby
    @end
   
    @end



@show
`\`\  `2She decided on `0`s10`2.  The husband studied his wife carefully.`\
@do write
`x`x
@choice
"That is a good name.  You have chosen very well, wife."
"I don't think so.  Try again."
@if response is 2 then goto namebaby
@do copytoname
@if sex is 1 then `s09 is she
@if sex is 0 then `s09 is he
@show
`c  `r1 `%ARE YOU READY? `r0

  `0Cunning.  `4Guileful.  `0Friendship.  `4Deceit.  `0Honor.  `4Disgrace.

  `%Life is what you make it.

  `0Return to the adventure:

   ˛ `2killing and loving
   `0˛ `2questing with other players
   `0˛ `2traveling a huge land with diverse terrain
   `0˛ `2purchase and upgrade your own dwelling
  `0 ˛ `2realtime online battles, chat & item trade
  `0 ˛`2 follow your heart and find your own path

`k
@show
`c `r1 `%SOME YEARS LATER `r0

  `0One day you hear your mother shrieking from the kitchen of your small
  cottage.

  `2You'd better get out of bed and find out what she wants.

  `|`b* `%NOTE `b*

  `2This game requires a fully `2A`3N`4S`5I `2compliant terminal.  If screens appear
  jumbled `8- `2we suggest you try another terminal program, as this game is
  not compatible with all of them.  `%ALSO`2:  Instead of arrow keys, you may
  also use the `$numeric keypad`2, turn numlock on to do this.

@KEY
;Give them starting stuff
  @do `p01 is 1
  @do `p02 is 10
  @do `p03 is 10
  @do `p04 is 5
  @do `p05 is 1
  @do `p09 is 1
  @do X is 27
  @do Y is 7
  @do MAP is 155
  @addchar

@#seperate
@writefile lognow.txt
                                 `2-`0=`2-`%=`2-`0=`2-
@closescript

@#DONE
