# OVB-2 Clone for Synchronet BBS v2.00x
# OVB-2.SRC
# By: Xsword
#     NEOCOM CBB&FL
#     703.666.9533
#     
#
############################################################################

:main
cmd_home
inc_main_cmds
node_action main
compare_user_misc EXPERT
    if_false
        cls
        menu \ovb-2\main
    end_if

async
crlf
print "ngMain cþh@TLEFT@ncþ"
print " hkCngohmnmhand: "
compare_user_misc COLDKEYS
    if_false
        getkeye
    else
        getstrupr 40
    end_if

cmdkey  "
end_cmd

cmdkey ^M
end_cmd

cmdkey ^@
end_cmd

cmdkey ^S
end_cmd

compare_user_misc COLDKEYS
    if_false
        printkey
    end_if

cmdkey ;
    compare_user_misc COLDKEYS
        if_true
            shift_str 1
        else
            getstrupr 40
        end_if
    exec_bin str_cmds
end_cmd

cmdkey A
    auto_message
end_cmd

# Your BBS listing Programs internal code goes here
# if it isn't the same as mine (i.e. LIST)
cmdkey B
    exec_xtrn sbl
end_cmd

cmdkey C
    page_sysop
end_cmd

cmdkey D
    userlist_logons
end_cmd

cmdkey E
    call email
end_cmd

cmdkey F
    setstr 1
    mail_send_feedback
end_cmd

cmdkey G
    logoff
end_cmd

cmdkey /G
    logoff_fast
end_cmd

cmdkey H
    nodelist_users
end_cmd

cmdkey K
    user_defaults
end_cmd

cmdkey L
    userlist_all
end_cmd

cmdkey M
    call message
end_cmd

cmdkey P
    xtrn_section
end_cmd

cmdkey Q
    call quick
end_cmd

cmdkey S
    info_system
end_cmd

cmdkey T
    call files
end_cmd

cmdkey I
    chat_section
end_cmd

cmdkey U
    minute_bank
end_cmd

cmdkey W
    printfile ..\data\logon.lst
    crlf
    pause
end_cmd

cmdkey Y
    info_user
end_cmd

cmdkey Z
    msg_ptrs_reinit
end_cmd

cmdkey +
    user_defaults
end_cmd

cmdkey !
    compare_ARS LEVEL 90
        if_true
        cls
        menu sysmain
        end_if
end_cmd

cmdkey ?
    compare_user_misc EXPERT
        if_true
            menu \ovb-2\main
        end_if
end_cmd

cmdkey $
    crlf
    chkfile ..\data\donation.txt
        if_true
            printfile ..\data\donation.txt
            crlf
            pause
        else
            print "\r\nnhPlease ask the SysOp about making a donation."
            crlf
            pause
        end_if
    end_cmd

print "\r\nnihrUnknown Command!n"
    compare_user_misc EXPERT
        if_true
            print "\r\nnhgHit \"c?g\" for menu.n"
            crlf
        end_if

end_cmd

Print "\r\nnhError is Command Shell!"
print "\r\nPlease Notify SysOp!"
crlf
pause
hangup

########################################################################
# FILE TRANSFER MENU
########################################################################

:files
cmd_home
inc_file_cmds
compare_user_misc EXPERT
    if_false
        cls
        menu \ovb-2\files
        crlf
    end_if

async
crlf
print "ngFile cþ h@TLEFT@ ncþ hc(@LN@) @LIB@ (@DN@) @DIR@: "

compare_user_misc COLDKEYS
    if_false
        getkeye
    else
        getstrupr 40
    end_if

logkey_comma

cmdkey digit
    compare_user_misc COLDKEYS
        if_false
            ungetkey
        end_if
    file_get_dir_num
end_cmd

cmdkey edigit
    compare_user_misc COLDKEYS
        if_false
            ungetkey
        else
            shift_str 1
        end_if
     file_get_lib_num
end_cmd

cmdkey  "
end_cmd

cmdkey ^S
end_cmd

cmdkey ^@
end_cmd

cmdkey ^M
end_cmd

compare_user_misc COLDKEYS
    if_false
        printkey
    end_if

cmdkey ;
    compare_user_misc COLDKEYS
        if_true
            shift_str 1
        else
            getstrupr 40
        end_if
        exec_bin str_cmds
end_cmd


cmdkey A
    file_select_area
end_cmd

cmdkey B
    file_batch_section
end_cmd

cmdkey C
    file_ptrs_cfg
end_cmd

cmdkey D
    file_download_batch
        if_false
            getfilespec
                if_true
                    file_download
                end_if
         end_if
end_cmd

cmdkey E
    getfilespec
        if_true
            file_remove
        end_if
end_cmd

cmdkey G
    logoff
end_cmd

cmdkey /G
    logoff_fast
end_cmd

cmdkey L
    getfilespec
        if_true
            file_list
        end_if
end_cmd

cmdkey M
    call message
end_cmd

cmdkey N
    file_new_scan
end_cmd

cmdkey Q
    cmd_pop
    return
end_cmd

cmdkey R
    file_batch_section
end_cmd

cmdkey S
    crlf
    mnemonics "Search by ~File Name or ~Text: "
    crlf
    print "nhCommand: "
    getcmd "FT"

        cmdkey F
            file_find_name
        end_cmd

        cmdkey T
            file_find_text
        end_cmd
end_cmd

cmdkey T
    file_temp_section
end_cmd

cmdkey U
    file_upload
        if_false
        crlf
            print "\r\nhUpload Aborted!"
            pause
        end_if
end_cmd

cmdkey V
    getfilespec
        if_true
            file_view
        end_if
end_cmd

cmdkey W
    getfilespec
        if_true
            file_list_extended
        end_if
end_cmd

cmdkey X
    chkfile ..\data\syswant.txt
        if_true
            crlf
            printfile ..\data\syswant.txt
            pause
        else
            crlf
            crlf
            print "nhNo particular files are being sought."
            crlf
            pause
        end_if
end_cmd

cmdkey ?
    compare_user_misc EXPERT
        if_true
            menu \ovb-2\files
        end_if
end_cmd

cmdkey /U
    file_upload_user
end_cmd

cmdkey /D
    file_download_user
end_cmd

cmdkey *
    file_show_directories
end_cmd

cmdkey /*
    file_show_libraries
end_cmd

cmdkey +
    file_directory_up
end_cmd

cmdkey -
    file_directory_down
end_cmd

cmdkey /+
    file_library_up
end_cmd

cmdkey /-
    file_library_down
end_cmd

cmdkey !
    compare_ARS LEVEL=90
        if_true
            cls
            menu sysmain
        end_if
end_cmd

print "\r\nnihrUnknown Command!"
crlf
compare_user_misc EXPERT
    if_true
        print "ngHit \"c?g\" for menu.n"
    end_if
end_cmd

cls
print "nhError in Command Shell!"
crlf
print "Please Notify SysOp!"
crlf
pause
hangup

##########################################################################
# QUICK MENU
##########################################################################

:quick
cmd_home
compare_user_misc EXPERT
    if_false
        cls
        menu \ovb-2\quick
    end_if

async
crlf
print "hkQuick ngþh@TLEFT@ngþ"
print " hkCngohmnmhand: "

compare_user_misc COLDKEYS
    if_false
        getkeye
    else
        getstrupr 40
    end_if

logkey_comma

cmdkey  "
end_cmd

cmdkey ^S
end_cmd

cmdkey ^@
end_cmd

cmdkey ^M
end_cmd

compare_user_misc COLDKEYS
    if_false
        printkey
    end_if

cmdkey C
    page_sysop
end_cmd

cmdkey E
    call email
end_cmd

cmdkey F
    setstr 1
    mail_send_feedback
end_cmd

cmdkey G
    logoff
end_cmd

cmdkey /G
    logoff_fast
end_cmd

cmdkey M
    call message
end_cmd

cmdkey Q
    cmd_pop
    return
end_cmd

cmdkey T
    call files
end_cmd

cmdkey ?
    compare_user_misc EXPERT
        if_true
            menu \ovb-2\quick
        end_if
end_cmd

print "\r\nnirhUnknown Command!"
crlf
compare_user_misc EXPERT
    if_true
        print "nhgHit \"c?g\" for menu."
    end_if
end_cmd

cls
print "nhError in Command Shell!"
crlf
print "Please notify SysOp!"
crlf
pause
hangup

###########################################################################
# MESSAGE MENU
###########################################################################

:message
cmd_home
compare_user_misc EXPERT
    if_false
        cls
        menu \ovb-2\message
    end_if

async
crlf
print "ngMessage cþ h@TLEFT@ ncþ ch[@GN@] @GRP@ [@SN@] @SUB@: "

compare_user_misc COLDKEYS
    if_false
        getkeye
    else
        getstrupr 40
    end_if

logkey_comma

cmdkey digit
    compare_user_misc COLDKEYS
        if_false
            ungetkey
        end_if
    msg_get_sub_num
end_cmd

cmdkey edigit
    compare_user_misc COLDKEYS
        if_false
            ungetkey
        else
            shift_str 1
        end_if
    msg_get_grp_num
end_cmd

cmdkey  "
end_cmd

cmdkey ^S
end_cmd

cmdkey ^@
end_cmd

cmdkey ^M
end_cmd

compare_user_misc COLDKEYS
    if_false
        printkey
    end_if

cmdkey A
    msg_select_area
end_cmd

cmdkey B
    msg_show_subboards
end_cmd

cmdkey C
    msg_new_scan_cfg
end_cmd

cmdkey G
    logoff
end_cmd

cmdkey /G
    logoff_fast
end_cmd

cmdkey J
    msg_show_groups
    print "\r\nWhich Group #? "
        compare_user_misc COLDKEYS
            if_false
                getkeye
                ungetkey
            else
                getstr 5
            end_if
         msg_get_grp_num
end_cmd

cmdkey N
    msg_new_scan
end_cmd

cmdkey O
    msg_qwk
end_cmd

cmdkey P
    msg_post
end_cmd

cmdkey Q
    cmd_pop
    return
end_cmd

cmdkey R
    msg_read
end_cmd

cmdkey T
    call files
end_cmd

cmdkey Y
    msg_your_scan
end_cmd

cmdkey Z
    toggle_user_misc new_sscan
end_cmd

cmdkey ?
    compare_user_misc EXPERT
        if_true
            menu \ovb-2\message
        end_if
end_cmd

cmdkey +
    msg_subboard_up
end_cmd

cmdkey -
    msg_subboard_down
end_cmd

cmdkey >
    msg_group_up
end_cmd

cmdkey <
    msg_group_down
end_cmd

cmdkey *
    msg_show_subboards
end_cmd

cmdkey /*
    msg_show_groups
end_cmd

print "\r\nnhriUnknown Command!"
crlf
compare_user_misc EXPERT
    if_true
        print "nhgHit \"c?g\" for menu."
    end_if
end_cmd

cls
print "nhError in Command Shell!"
crlf
print "Please Notify SysOp!"
crlf
pause
hangup

##########################################################################
# EMAIL MENU
##########################################################################

:email
cmd_home
compare_user_misc EXPERT
    if_false
        cls
        menu \ovb-2\email
    end_if

async
crlf
print "hkEmail ngþhg@TLEFT@ngþ"
print " hkCngohmnmhand: n"

compare_user_misc COLDKEYS
    if_false
        getkeye
    else
        getstrupr 40
    end_if

logkey_comma

cmdkey  "
end_cmd

cmdkey ^M
end_cmd

cmdkey ^S
end_cmd

cmdkey ^@
end_cmd

compare_user_misc COLDKEYS
    if_false
        printkey
    end_if

cmdkey F
    crlf
    crlf
    print "nhbSend Mail/File to Who? "
    getname 25
    mail_send_file
        if_false
            compare_str SYSOP
                if_true
                    setstr 1
                    mail_send_file
                end_if
        end_if
end_cmd

cmdkey R
    mail_read
end_cmd

cmdkey S
    crlf
    crlf
    print "nhgSend Mail to Who? "
    getname 25
    mail_send
        if_false
            compare_str SYSOP
                if_true
                    setstr 1
                    mail_send
                 end_if
        end_if
end_cmd


cmdkey V
    mail_read_sent
end_cmd

cmdkey Q
    cmd_pop
    return
end_cmd

cmdkey ?
    compare_user_misc EXPERT
        if_true
            menu \ovb-2\email
        end_if
end_cmd

print "\r\nnirhUnknown Command!"
compare_user_misc EXPERT
    if_true
        crlf
        print "nghHit \"c?\" gfor menu.n"
    end_if
end_cmd

cls
print "nhError in Command Shell!"
crlf
print "Please notify SysOp!"
pause
hangup

# End of ovb-2.SRC
