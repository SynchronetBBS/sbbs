# matrix.src

# Light-bar answer module for Synchronet BBS v2.2+

# $Id: matrix.src,v 1.3 2000/10/26 11:36:52 rswindell Exp $

# @format.tab-size 8, @format.use-tabs true

!include sbbsdefs.inc
!define LAST 2
logout
node_status NODE_LOGON

compare_ars WIP
if_true
	exec_bin WIPLOGIN
	return
        end_if

int c m

:top
compare c 10
if_greater_or_equal
	hangup
	return
	end_if

# Trigger scripts with old style prompt
print "NN: "
pause_reset
cls
cmd_home
compare_user_misc UM_ANSI
if_true
	print "\x1b[H"
else
	cls
        end_if

print "                        "
print "\x014\x01y\x01h"
print "ษออออออออออออออออออออออออออออออป\x01n\r\n"
print "                        "
print "\x014\x01y\x01h"
print "บ         Logon Matrix         บ\x01n\x01bฐฐ\x01n\r\n"
print "                        "
print "\x014\x01y\x01h"
print "ฬออออออออออออออออออออออออออออออน\x01n\x01bฐฐ\x01n\r\n"
print "                        \x014\x01y\x01hบ "
compare m 0
	if_equal
		print "\x10\x017\x01b\x01h"
	else
		print " \x014\x01w\x01h"
	end_if
print " Existing User Account     "
print "\x014\x01y บ\x01n\x01bฐฐ\x01n\r\n                        \x014\x01y\x01hบ "
compare m 1
	if_equal
		print "\x10\x017\x01b\x01h"
	else
		print " \x014\x01w\x01h"
	end_if
print " New User Account          "
print "\x014\x01y บ\x01n\x01bฐฐ\x01n\r\n                        \x014\x01y\x01hบ "
compare m 2
	if_equal
		print "\x10\x017\x01b\x01h"
	else
		print " \x014\x01w\x01h"
	end_if
print " Guest User Account        "
print "\x014\x01y บ\x01n\x01bฐฐ\x01n\r\n                        \x014\x01y\x01h"
print "ศออออออออออออออออออออออออออออออผ\x01n\x01bฐฐ\x01n\r\n\x01n\x01b"
print "                         "
print " ฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐฐ\x01n\r\n"

#sync
getkey

# Down Arrow, Ctrl-J or 'D'
cmdkeys ^JD
	add m 1
	compare m LAST
	if_greater
		set m 0
		end_if
	pause_reset
	end_cmd

# Up Arrow, Ctrl-^ or 'U'
cmdkeys ^^U
	sub m 1
	compare m 0
	if_less
		set m LAST
		end_if
	pause_reset
	end_cmd

# Home or Ctrl-B
cmdkey ^B
	set m 0
	end_cmd

# End of Ctrl-E
cmdkey ^E
	set m LAST
        end_cmd

cmdkeys ABCDEFGHIJKLMNOPQRSTUVWXYZ*
	ungetkey
	cmd_pop
	end_cmd

switch m
	case 0
		cls
		print "Login: "
		getstr str 25 K_UPRLWR|K_LOWPRIO|K_E71DETECT
		truncsp str
		compare_str ""
		if_true
			add c 1
			goto top
			end_if
		login "PW: "
		if_true
			logon
			if_true
				return
				end_if
			end_if
		pause
		add c 2
		goto top
		end_case
	case 1
		setstr "%q"
		compare_str "VERT"
		if_true
			yes_no Are you calling just to download Synchronet BBS software
			if_true
				setstr "Guest"
				login "PW: "
				if_true
					logon
					if_true
						return
						end_if
					end_if
				end_if
			end_if
		newuser
		logon
		return
		end_case

	case 2
		setstr "guest"
		login "PW: "
		if_true
			logon
			if_true
				return
				end_if
			end_if
		end_case
	end_switch
goto top

